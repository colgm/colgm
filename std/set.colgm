use std::str::{ str };
use std::libc::{ malloc, realloc, free };
use std::list::{ list, list_iter };
use std::io::{ io };
use std::panic::{ panic };

pub struct hashset<T> {
    size: u64,
    bucket_capacity: u64,
    bucket: list<T>*
}

impl hashset<T> {
    pub func new() -> hashset<T>* {
        var res = hashset<T>::__alloc__();
        if (res == nil) {
            panic("failed to allocate memory");
        }
        res->init();
        return res;
    }

    pub func instance() -> hashset<T> {
        var res = hashset<T> {};
        res.init();
        return res;
    }

    pub func clone(self) -> hashset<T> {
        var res = hashset<T>::instance();
        foreach (var i; self) {
            res.insert(i.elem());
        }
        return res;
    }

    func init(self) {
        self.size = 0;
        self.bucket_capacity = 32;
        self.bucket = malloc(self.bucket_capacity * list<T>::__size__()) => list<T>*;
        for (var i: u64 = 0; i < self.bucket_capacity; i += 1) {
            self.bucket[i] = list<T>::instance();
        }
    }

    pub func delete(self) {
        for (var i: u64 = 0; i < self.bucket_capacity; i += 1) {
            self.bucket[i].delete();
        }
        free(self.bucket => i8*);
        self.size = 0;
        self.bucket_capacity = 0;
        self.bucket = nil;
    }

    pub func clear(self) {
        for (var i: u64 = 0; i < self.bucket_capacity; i += 1) {
            self.bucket[i].clear();
        }
        self.size = 0;
    }
}

impl hashset<T> {
    #[is_non_trivial(T)]
    pub func has(self, item: T&) -> bool {
        var hash = item.hash() % self.bucket_capacity;
        var bucket = self.bucket[hash].__ptr__();
        var tmp = bucket->head;
        while (tmp != nil) {
            if (tmp->elem.eq(item)) {
                return true;
            }
            tmp = tmp->next;
        }
        return false;
    }

    #[is_trivial(T)]
    pub func has(self, item: T) -> bool {
        var hash = item.hash() % self.bucket_capacity;
        var bucket = self.bucket[hash].__ptr__();
        var tmp = bucket->head;
        while (tmp != nil) {
            if (tmp->elem.eq(item)) {
                return true;
            }
            tmp = tmp->next;
        }
        return false;
    }

    pub func empty(self) -> bool {
        return self.size == 0;
    }

    #[is_non_trivial(T)]
    pub func insert(self, item: T&) {
        var hash = item.hash() % self.bucket_capacity;
        // must use pointer/reference, otherwise changes will not
        // be reflected in the hashset
        var bucket = self.bucket[hash].__ptr__();
        var tmp = bucket->head;
        while (tmp != nil) {
            if (tmp->elem.eq(item)) {
                return;
            }
            tmp = tmp->next;
        }
        bucket->insert(item);
        self.size += 1;

        if (self.size > self.bucket_capacity) {
            self.rehash();
        }
    }

    #[is_trivial(T)]
    pub func insert(self, item: T) {
        var hash = item.hash() % self.bucket_capacity;
        // must use pointer/reference, otherwise changes will not
        // be reflected in the hashset
        var bucket = self.bucket[hash].__ptr__();
        var tmp = bucket->head;
        while (tmp != nil) {
            if (tmp->elem.eq(item)) {
                return;
            }
            tmp = tmp->next;
        }
        bucket->insert(item);
        self.size += 1;

        if (self.size > self.bucket_capacity) {
            self.rehash();
        }
    }

    func rehash_pre_work(self) -> list<T> {
        var temp = list<T>::instance();
        foreach (var i; self) {
            temp.insert(i.elem());
        }

        self.clear();
        self.bucket_capacity *= 2;
        self.bucket = realloc(
            self.bucket => i8*,
            self.bucket_capacity *
            list<T>::__size__()
        ) => list<T>*;
        for (var i: u64 = 0; i < self.bucket_capacity / 2; i += 1) {
            self.bucket[i].clear();
        }
        for (var i = self.bucket_capacity / 2; i < self.bucket_capacity; i += 1) {
            self.bucket[i] = list<T>::instance();
        }

        return temp;
    }

    func rehash(self) {
        var temp = self.rehash_pre_work();
        defer temp.delete();

        foreach (var i; temp) {
            self.insert(i.elem());
        }
    }
}

struct iter<T> {
    set: hashset<T>*,
    bucket_index: u64,
    bucket_iter: list_iter<T>
}

impl iter<T> {
    func instance(set: hashset<T>*) -> iter<T> {
        var index: u64 = 0;
        var i = set->bucket[0].iter();
        while (i.is_end()) {
            index += 1;
            if (index >= set->bucket_capacity) {
                break;
            }
            i = set->bucket[index].iter();
        }
        return iter<T> {
            set: set,
            bucket_index: index,
            bucket_iter: i
        };
    }

    pub func is_end(self) -> bool {
        return self.bucket_index >= self.set->bucket_capacity;
    }

    #[is_non_trivial(T)]
    pub func elem(self) -> T& {
        return self.bucket_iter.elem();
    }

    #[is_trivial(T)]
    pub func elem(self) -> T {
        return self.bucket_iter.elem();
    }

    pub func next(self) -> iter<T> {
        if (!self.bucket_iter.is_end()) {
            self.bucket_iter = self.bucket_iter.next();
        }

        while (self.bucket_iter.is_end()) {
            self.bucket_index += 1;
            if (self.bucket_index >= self.set->bucket_capacity) {
                break;
            }
            self.bucket_iter = self.set->bucket[self.bucket_index].iter();
        }

        return iter<T> {
            set: self.set,
            bucket_index: self.bucket_index,
            bucket_iter: self.bucket_iter,
        };
    }
}

impl hashset<T> {
    pub func iter(self) -> iter<T> {
        return iter<T>::instance(self.__ptr__());
    }
}