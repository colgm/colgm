use std::map::{ hashmap };
use std::set::{ hashset };
use std::vec::{ vec };
use std::str::{ str };
use std::io::{ io };
use std::panic::{ panic };

use err::report::{ report };
use err::span::{ span };
use util::package::{ package };

use sema::function::{ colgm_func };
use sema::primitive::{ colgm_primitive };
use sema::module::{ colgm_module };
use sema::type::{ type };
use sema::symbol_info::{ symbol_info };

pub struct global_symbol_table {
    main_input_file: str,
    constant_string: hashset<str>,
    primitives: hashmap<str, colgm_primitive>,
    domain: hashmap<str, colgm_module>
}

impl global_symbol_table {
    pub func instance() -> global_symbol_table {
        return global_symbol_table {
            main_input_file: str::instance(),
            constant_string: hashset<str>::instance(),
            primitives: hashmap<str, colgm_primitive>::instance(),
            domain: hashmap<str, colgm_module>::instance()
        };
    }

    pub func delete(self) {
        self->main_input_file.delete();
        self->constant_string.delete();
        self->primitives.delete();
        self->domain.delete();
    }

    pub func create_domain_if_not_exist(self, name: str*) {
        if (self->domain.has(name)) {
            return;
        }

        var res = colgm_module::instance();
        defer res.delete();

        self->domain.insert(name, res.__ptr__());
    }
}

struct local_variable {
    type: type,
    location: span,
    used: bool
}

impl local_variable {
    pub func instance(t: type*, l: span*) -> local_variable {
        return local_variable {
            type: t->clone(),
            location: l->clone(),
            used: false
        };
    }

    pub func delete(self) {
        self->type.delete();
        self->location.delete();
    }

    pub func clone(self) -> local_variable {
        return local_variable {
            type: self->type.clone(),
            location: self->location.clone(),
            used: self->used
        };
    }
}

pub struct sema_context {
    global: global_symbol_table*,
    this_file: str,
    generics: hashset<str>,
    constant_string: hashset<str>,
    local_scope: vec<hashmap<str, local_variable>>
}

impl sema_context {
    pub func push_scope_level(self) {
        var res = hashmap<str, local_variable>::instance();
        defer res.delete();

        self->local_scope.push(res.__ptr__());
    }

    pub func pop_scope_level(self, err: report*) {
        foreach (var v; self->local_scope.back()) {
            var name = v.key();
            var lv = v.value();
            if (lv->used) {
                continue;
            }
            if (name->eq_const("self")) {
                continue;
            }
            err->report_unused_variable(lv->location.__ptr__(), name->c_str);
        }
        self->local_scope.pop_back();
    }

    pub func add_local_var(self, name: str*, t: type*, loc: span*) {
        var lv = local_variable::instance(t, loc);
        defer lv.delete();
        
        self->local_scope.back()->insert(name, lv.__ptr__());
    }

    pub func add_var_used(self, name: str*) {
        if (self->local_scope.empty()) {
            return;
        }
        for (var i = self->local_scope.size - 1; (i => i64) >= 0; i -= 1) {
            if (self->local_scope.get(i)->has(name)) {
                self->local_scope.get(i)->get(name)->used = true;
                return;
            }
        }
    }

    pub func get_local(self, name: str*) -> type* {
        if (self->local_scope.empty()) {
            return nil;
        }
        for (var i = self->local_scope.size - 1; (i => i64) >= 0; i -= 1) {
            if (self->local_scope.get(i)->has(name)) {
                return self->local_scope.get(i)->get(name)->type.__ptr__();
            }
        }
        return nil;
    }

    pub func get_local_def_location(self, name: str*) -> span* {
        if (self->local_scope.empty()) {
            return nil;
        }
        for (var i = self->local_scope.size - 1; (i => i64) >= 0; i -= 1) {
            if (self->local_scope.get(i)->has(name)) {
                return self->local_scope.get(i)->get(name)->location.__ptr__();
            }
        }
        return nil;
    }

    pub func find_local(self, name: str*) -> bool {
        if (self->local_scope.empty()) {
            return false;
        }
        for (var i = self->local_scope.size - 1; (i => i64) >= 0; i -= 1) {
            if (self->local_scope.get(i)->has(name)) {
                return true;
            }
        }
        return false;
    }
}

impl sema_context {
    pub func global_symbol(self) -> hashmap<str, symbol_info>* {
        return self->get_domain(self->this_file.__ptr__())
                   ->global_symbol
                   .__ptr__();
    }

    pub func generic_symbol(self) -> hashmap<str, symbol_info>* {
        return self->get_domain(self->this_file.__ptr__())
                   ->generic_symbol
                   .__ptr__();
    }
}

impl sema_context {
    pub func instance(g: global_symbol_table*) -> sema_context {
        return sema_context {
            global: g,
            this_file: str::instance(),
            generics: hashset<str>::instance(),
            constant_string: hashset<str>::instance(),
            local_scope: vec<hashmap<str, local_variable>>::instance()
        };
    }

    pub func delete(self) {
        self->this_file.delete();
        self->generics.delete();
        self->constant_string.delete();
        self->local_scope.delete();
    }

    pub func dump(self, out: io, pkg: package*) {
        var gt = self->global;
        if (gt->primitives.size > 0) {
            out.cyan().out("primitives:\n").reset();
        }
        foreach (var i; gt->primitives) {
            out.out("  ");
            i.value()->dump(out);
        }

        foreach (var i; gt->domain) {
            i.value()->dump(out, pkg);
            out.endln();
        }
    }
}

impl sema_context {
    pub func create_domain_if_not_exist(self, name: str*) {
        self->global->create_domain_if_not_exist(name);
    }

    pub func has_domain(self, name: str*) -> bool {
        return self->global->domain.has(name);
    }

    pub func get_domain(self, name: str*) -> colgm_module* {
        if (self->has_domain(name)) {
            return self->global->domain.get(name);
        }

        panic("domain does not exist");
        return nil;
    }
}