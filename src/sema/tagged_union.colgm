use std::str::{ str };
use std::vec::{ vec };
use std::map::{  hashmap };
use std::io::{ io };

use err::span::{ span };
use err::panic::{ panic };
use util::package::{ package };

use sema::type::{ type };

pub struct colgm_tagged_union {
    name: str,
    ref_enum_type: type,
    location: span,

    members: hashmap<str, type>,
    member_int_map: hashmap<str, i64>,
    ordered_members: vec<str>,

    is_public: bool,
    is_extern: bool
}

impl colgm_tagged_union {
    pub func new(name: str*, loc: span*) -> colgm_tagged_union* {
        var res = colgm_tagged_union::__alloc__();
        if (res == nil) {
            panic("failed to allocate memory");
        }
        res->name = name->copy_instance();
        res->ref_enum_type = type::instance();
        res->location = loc->copy_instance();
        res->members = hashmap<str, type>::instance();
        res->member_int_map = hashmap<str, i64>::instance();
        res->ordered_members = vec<str>::instance();
        res->is_public = false;
        res->is_extern = false;
        return res;
    }

    pub func delete(self) {
        self->name.delete();
        self->ref_enum_type.delete();
        self->location.delete();
        self->members.delete();
        self->member_int_map.delete();
        self->ordered_members.delete();
    }

    pub func copy(self) -> colgm_tagged_union* {
        var res = colgm_tagged_union::__alloc__();
        if (res == nil) {
            panic("failed to allocate memory");
        }
        res->name = self->name.copy_instance();
        res->ref_enum_type = self->ref_enum_type.copy_instance();
        res->location = self->location.copy_instance();
        res->members = self->members.copy_instance();
        res->member_int_map = self->member_int_map.copy_instance();
        res->ordered_members = self->ordered_members.copy_instance();
        res->is_public = self->is_public;
        res->is_extern = self->is_extern;
        return res;
    }

    pub func copy_instance(self) -> colgm_tagged_union {
        return colgm_tagged_union {
            name: self->name.copy_instance(),
            ref_enum_type: self->ref_enum_type.copy_instance(),
            location: self->location.copy_instance(),
            members: self->members.copy_instance(),
            member_int_map: self->member_int_map.copy_instance(),
            ordered_members: self->ordered_members.copy_instance(),
            is_public: self->is_public,
            is_extern: self->is_extern
        };
    }
}

impl colgm_tagged_union {
    pub func has_ref_enum(self) -> bool {
        return !self->ref_enum_type.name.empty();
    }

    pub func dump(self, out: io, pkg: package*) {
        out.out("  ");
        if (self->is_extern) {
            out.green().out("extern ").reset();
        }
        if (self->is_public) {
            out.green().out("pub ").reset();
        }
        out.cyan().out("union ").reset();
        if (self->has_ref_enum()) {
            out.light_orange().out("(").reset();
            self->ref_enum_type.dump(out, pkg);
            out.light_orange().out(") ").reset();
        }
        out.white().out(self->name.c_str).reset();

        if (self->ordered_members.empty()) {
            out.out(" {}\n");
            return;
        }
        out.out(" {\n");
        forindex (var i; self->ordered_members) {
            var member = self->ordered_members.get(i);
            out.out("    ").out(member->c_str);
            // dump mapped int
            out.light_green().out("(").out_i64(self->member_int_map.get(member));
            out.out(")").reset().out(": ");
            // dump type
            self->members.get(member)->dump(out, pkg);
            out.out("\n");
        }
        out.out("  }\n");
    }
}