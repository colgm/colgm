use std::str::{ str };
use std::libc::{ free };
use std::vec::{ vec, primitive_vec };
use std::map::{ hashmap };
use std::ptr::{ ptr };
use std::io::{ io };

use err::span::{ span };
use err::panic::{ panic };
use ast::ast::{ ast, ast_struct_decl, ast_impl };
use package::{ package };

use sema::type::{ type, symbol };
use sema::function::{ colgm_func };

pub struct colgm_struct {
    name: str,
    location: span,
    fields: hashmap<str, type>,
    ordered_fields: vec<symbol>,
    static_method: hashmap<str, colgm_func>,
    method: hashmap<str, colgm_func>,

    generic_template: vec<str>,
    generic_struct_decl: ast_struct_decl*,
    generic_struct_impl: primitive_vec<ptr<ast_impl>>,

    is_public: bool,
    is_extern: bool
}

impl colgm_struct {
    pub func new(name: str*, loc: span*) -> colgm_struct* {
        var res = colgm_struct::__alloc__();
        if (res == nil) {
            panic("colgm_struct::new: failed to allocate memory");
        }
        res->name = name->copy_instance();
        res->location = loc->copy_instance();
        res->fields = hashmap<str, type>::instance();
        res->ordered_fields = vec<symbol>::instance();
        res->static_method = hashmap<str, colgm_func>::instance();
        res->method = hashmap<str, colgm_func>::instance();
        res->generic_template = vec<str>::instance();
        res->generic_struct_decl = nil;
        res->generic_struct_impl = primitive_vec<ptr<ast_impl>>::instance();
        res->is_public = false;
        res->is_extern = false;
        return res;
    }

    pub func delete(self) {
        self->name.delete();
        self->location.delete();
        self->fields.delete();
        self->ordered_fields.delete();
        self->static_method.delete();
        self->method.delete();
        self->generic_template.delete();
        if (self->generic_struct_decl != nil) {
            var to_ast = self->generic_struct_decl => ast*;
            to_ast->delete();
            free(to_ast => i8*);
            self->generic_struct_decl = nil;
        }
        for (var i: u64 = 0; i < self->generic_struct_impl.size; i += 1) {
            var to_ast = self->generic_struct_impl.get(i).unwrap() => ast*;
            to_ast->delete();
            free(to_ast => i8*);
        }
        self->generic_struct_impl.delete();
    }

    pub func copy(self) -> colgm_struct* {
        var res = colgm_struct::__alloc__();
        res->name = self->name.copy_instance();
        res->location = self->location.copy_instance();
        res->fields = self->fields.copy_instance();
        res->ordered_fields = self->ordered_fields.copy_instance();
        res->static_method = self->static_method.copy_instance();
        res->method = self->method.copy_instance();
        res->generic_template = self->generic_template.copy_instance();
        if (self->generic_struct_decl != nil) {
            res->generic_struct_decl = self->generic_struct_decl->copy();
        } else {
            res->generic_struct_decl = nil;
        }
        res->generic_struct_impl = primitive_vec<ptr<ast_impl>>::instance();
        for (var i: u64 = 0; i < self->generic_struct_impl.size; i += 1) {
            res->generic_struct_impl.push(
                ptr<ast_impl>::wrap(
                    self->generic_struct_impl.get(i).unwrap()->copy()
                )
            );
        }
        res->is_public = self->is_public;
        res->is_extern = self->is_extern;
        return res;
    }
}

impl colgm_struct {
    pub func dump(self, out: io, pkg: package*) {
        out.out("  ");
        if (self->is_extern) {
            out.green().out("extern ").reset();
        }
        if (self->is_public) {
            out.green().out("pub ").reset();
        }
        out.cyan().out("struct ").reset();
        out.white().out(self->name.c_str).reset();

        // print generic template
        if (self->generic_template.size > 0) {
            out.out("<");
            for (var i: u64 = 0; i < self->generic_template.size; i += 1) {
                out.out(self->generic_template.get(i)->c_str);
                if (i != self->generic_template.size - 1) {
                    out.out(", ");
                }
            }
            out.out(">");
        }

        if (self->ordered_fields.empty() &&
            self->static_method.empty() &&
            self->method.empty()) {
            out.out(" {}\n");
            return;
        }
        out.out(" {\n");
        for (var i: u64 = 0; i < self->ordered_fields.size; i += 1) {
            var field = self->ordered_fields.get(i);
            out.out("    ").out(field->name.c_str).out(": ");
            field->sym_type.dump(out, pkg);
            out.out("\n");
        }
        for (var i = self->method.iter(); !i.is_end(); i = i.next()) {
            i.value()->dump(out, pkg, "  ");
        }
        for (var i = self->static_method.iter(); !i.is_end(); i = i.next()) {
            i.value()->dump(out, pkg, "  ");
        }
        out.out("  }\n");
    }
}