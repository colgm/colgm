use std::str::{ str };
use std::io::{ io };

enum indent_state {
    indent_none,
    indent_middle,
    indent_bar,
    indent_end
}

pub struct tree_maker {
    indent: str
}

impl tree_maker {
    pub func instance() -> tree_maker {
        return tree_maker { indent: str::instance() };
    }

    pub func delete(self) {
        self->indent.delete();
    }

    func indent_back(self) -> indent_state {
        var s = self->indent.size;
        if (s == 0) {
            return indent_state::indent_none;
        }
        if (self->indent.c_str[s - 1] == '-' &&
            self->indent.c_str[s - 2] == '|') {
            return indent_state::indent_middle;
        }
        if (self->indent.c_str[s - 1] == ' ' &&
            self->indent.c_str[s - 2] == '|') {
            return indent_state::indent_bar;
        }
        if (self->indent.c_str[s - 1] == '-' &&
            self->indent.c_str[s - 2] == '`') {
            return indent_state::indent_end;
        }
        return indent_state::indent_none;
    }

    pub func push_indent(self) {
        if (self->indent.size > 0) {
            var state = self->indent_back();
            match (state) {
                indent_state::indent_none => {}
                indent_state::indent_middle => {
                    self->indent.pop_back();
                    self->indent.pop_back();
                    self->indent.append_i8_vec("| ");
                }
                indent_state::indent_bar => {}
                indent_state::indent_end => {
                    self->indent.pop_back();
                    self->indent.pop_back();
                    self->indent.append_i8_vec("  ");
                }
            }
        }
        self->indent.append_i8_vec("|-");
    }

    pub func set_last(self) {
        self->indent.pop_back();
        self->indent.pop_back();
        self->indent.append_i8_vec("`-");
    }

    pub func pop_indent(self) {
        self->indent.pop_back();
        self->indent.pop_back();
    }

    pub func dump_indent(self, out: io) {
        if (self->indent.size > 0 &&
            self->indent_back() == indent_state::indent_bar) {
            self->indent.pop_back();
            self->indent.pop_back();
            self->indent.append_i8_vec("|-");
        }
        out.out(self->indent.c_str);
    }
}