use std::libc::{ malloc, realloc, free };
use std::ptr::{ ptr_size };
use err::panic::{ panic };

pub struct vec<T> {
    data: T**,
    size: u64,
    capacity: u64
}

pub struct primitive_vec<T> {
    data: T*,
    size: u64,
    capacity: u64
}

impl vec<T> {
    pub func new() -> vec<T>* {
        var res = vec<T>::__alloc__();
        if (res == nil) {
            panic("failed to allocate memory");
        }
        res->size = 0;
        res->capacity = 4;
        res->data = malloc(res->capacity * ptr_size()) => T**;
        for (var i: u64 = 0; i < res->capacity; i += 1) {
            res->data[i] = nil;
        }
        return res;
    }

    pub func instance() -> vec<T> {
        var res = vec<T> {
            size: 0,
            capacity: 4,
            data: nil
        };
        res.data = malloc(res.capacity * ptr_size()) => T**;
        for (var i: u64 = 0; i < res.capacity; i += 1) {
            res.data[i] = nil;
        }
        return res;
    }

    pub func copy(self) -> vec<T>* {
        var res = vec<T>::new();
        for (var i: u64 = 0; i < self->size; i += 1) {
            res->push(self->data[i]);
        }
        return res;
    }

    pub func copy_instance(self) -> vec<T> {
        var res = vec<T>::instance();
        for (var i: u64 = 0; i < self->size; i += 1) {
            res.push(self->data[i]);
        }
        return res;
    }

    pub func clear(self) {
        for (var i: u64 = 0; i < self->size; i += 1) {
            self->data[i]->delete();
            free(self->data[i] => i8*);
            self->data[i] = nil;
        }
        self->size = 0;
    }

    pub func delete(self) {
        for (var i: u64 = 0; i < self->size; i += 1) {
            self->data[i]->delete();
            free(self->data[i] => i8*);
        }
        free(self->data => i8*);
        self->data = nil;
        self->size = 0;
        self->capacity = 0;
    }

    pub func empty(self) -> bool {
        return self->size == 0;
    }

    func extend_capacity(self) {
        self->capacity *= 2;
        self->data = realloc(
            self->data => i8*,
            self->capacity * ptr_size()
        ) => T**;
    }

    pub func push(self, item: T*) {
        if (self->size >= self->capacity) {
            self->extend_capacity();
        }
        self->data[self->size] = item->copy();
        self->size += 1;
    }

    pub func pop_back(self) {
        if (self->size == 0) {
            return;
        }
        self->size -= 1;
        self->data[self->size]->delete();
        free(self->data[self->size] => i8*);
        self->data[self->size] = nil;
    }

    pub func back(self) -> T* {
        if (self->size == 0) {
            panic("empty vector");
        }
        return self->data[self->size - 1];
    }

    pub func get(self, index: u64) -> T* {
        if (index >= self->size) {
            panic("index out of bounds");
        }
        return self->data[index];
    }

    pub func iter(self) -> iter<T> {
        return iter<T>::instance(self);
    }
}

struct iter<T> {
    _vec: vec<T>*,
    _index: u64
}

impl iter<T> {
    pub func instance(v: vec<T>*) -> iter<T> {
        return iter<T> {
            _vec: v,
            _index: 0
        };
    }

    pub func is_end(self) -> bool {
        return self->_index >= self->_vec->size;
    }

    pub func next(self) -> iter<T> {
        if (self->_index >= self->_vec->size) {
            return iter<T> {
                _vec: self->_vec,
                _index: self->_index
            };
        }
        return iter<T> {
            _vec: self->_vec,
            _index: self->_index + 1
        };
    }

    pub func get(self) -> T* {
        return self->_vec->get(self->_index);
    }

    pub func index(self) -> u64 {
        return self->_index;
    }
}

impl primitive_vec<T> {
    pub func new() -> primitive_vec<T>* {
        var res = primitive_vec<T>::__alloc__();
        if (res == nil) {
            panic("failed to allocate memory");
        }
        res->size = 0;
        res->capacity = 4;
        res->data = malloc(res->capacity * T::__size__()) => T*;
        return res;
    }

    pub func instance() -> primitive_vec<T> {
        var res = primitive_vec<T> {
            size: 0,
            capacity: 4,
            data: nil
        };
        res.data = malloc(res.capacity * T::__size__()) => T*;
        return res;
    }

    pub func copy(self) -> primitive_vec<T>* {
        var res = primitive_vec<T>::new();
        for (var i: u64 = 0; i < self->size; i += 1) {
            res->push(self->data[i]);
        }
        return res;
    }

    pub func copy_instance(self) -> primitive_vec<T> {
        var res = primitive_vec<T>::instance();
        for (var i: u64 = 0; i < self->size; i += 1) {
            res.push(self->data[i]);
        }
        return res;
    }

    pub func clear(self) {
        self->size = 0;
    }

    pub func delete(self) {
        free(self->data => i8*);
        self->data = nil;
        self->size = 0;
        self->capacity = 0;
    }

    pub func empty(self) -> bool {
        return self->size == 0;
    }

    func extend_capacity(self) {
        self->capacity *= 2;
        self->data = realloc(self->data => i8*, self->capacity * T::__size__()) => T*;
    }

    pub func push(self, item: T) {
        if (self->size >= self->capacity) {
            self->extend_capacity();
        }
        self->data[self->size] = item;
        self->size += 1;
    }

    pub func get(self, index: u64) -> T {
        return self->data[index];
    }

    pub func back(self) -> T {
        if (self->size == 0) {
            panic("empty vector");
        }
        return self->data[self->size - 1];
    }

    pub func pop_back(self) {
        if (self->size == 0) {
            panic("empty vector");
        }
        self->size -= 1;
    }

    pub func iter(self) -> pv_iter<T> {
        return pv_iter<T>::instance(self);
    }
}

struct pv_iter<T> {
    _vec: primitive_vec<T>*,
    _index: u64
}

impl pv_iter<T> {
    pub func instance(v: primitive_vec<T>*) -> pv_iter<T> {
        return pv_iter<T> {
            _vec: v,
            _index: 0
        };
    }

    pub func is_end(self) -> bool {
        return self->_index >= self->_vec->size;
    }

    pub func next(self) -> pv_iter<T> {
        if (self->_index >= self->_vec->size) {
            return pv_iter<T> {
                _vec: self->_vec,
                _index: self->_index
            };
        }
        return pv_iter<T> {
            _vec: self->_vec,
            _index: self->_index + 1
        };
    }

    pub func get(self) -> T {
        return self->_vec->get(self->_index);
    }

    pub func index(self) -> u64 {
        return self->_index;
    }
}