use std::libc::{ malloc, realloc, free };
use std::ptr::{ ptr_size };
use err::panic::{ panic };

pub struct vec<T> {
    data: T**,
    size: u64,
    capacity: u64
}

pub struct primitive_vec<T> {
    data: T*,
    size: u64,
    capacity: u64
}

impl vec<T> {
    pub func new() -> vec<T>* {
        var res = vec<T>::__alloc__();
        if (res == nil) {
            panic("vec<T>::new: failed to allocate memory");
        }
        res->size = 0;
        res->capacity = 4;
        res->data = malloc(res->capacity * ptr_size()) => T**;
        for (var i: u64 = 0; i < res->capacity; i += 1) {
            res->data[i] = nil;
        }
        return res;
    }

    pub func instance() -> vec<T> {
        var res = vec<T> {
            size: 0,
            capacity: 4,
            data: nil
        };
        res.data = malloc(res.capacity * ptr_size()) => T**;
        for (var i: u64 = 0; i < res.capacity; i += 1) {
            res.data[i] = nil;
        }
        return res;
    }

    pub func copy(self) -> vec<T>* {
        var res = vec<T>::new();
        for (var i: u64 = 0; i < self->size; i += 1) {
            res->push(self->data[i]);
        }
        return res;
    }

    pub func copy_instance(self) -> vec<T> {
        var res = vec<T>::instance();
        for (var i: u64 = 0; i < self->size; i += 1) {
            res.push(self->data[i]);
        }
        return res;
    }

    pub func clear(self) {
        for (var i: u64 = 0; i < self->size; i += 1) {
            self->data[i]->delete();
            free(self->data[i] => i8*);
            self->data[i] = nil;
        }
        self->size = 0;
    }

    pub func delete(self) {
        for (var i: u64 = 0; i < self->size; i += 1) {
            self->data[i]->delete();
            free(self->data[i] => i8*);
        }
        free(self->data => i8*);
        self->data = nil;
        self->size = 0;
        self->capacity = 0;
    }

    func extend_capacity(self) {
        self->capacity *= 2;
        self->data = realloc(
            self->data => i8*,
            self->capacity * ptr_size()
        ) => T**;
    }

    pub func push(self, item: T*) {
        if (self->size >= self->capacity) {
            self->extend_capacity();
        }
        self->data[self->size] = item->copy();
        self->size += 1;
    }

    pub func get(self, index: u64) -> T* {
        if (index >= self->size) {
            panic("std::vec::get: index out of bounds");
        }
        return self->data[index];
    }

    pub func iter(self) -> vec_iter<T> {
        return vec_iter<T>::instance(self);
    }
}

struct vec_iter<T> {
    vec: vec<T>*,
    _index: u64
}

impl vec_iter<T> {
    pub func instance(v: vec<T>*) -> vec_iter<T> {
        return vec_iter<T> {
            vec: v,
            _index: 0
        };
    }

    pub func is_end(self) -> bool {
        return self->_index >= self->vec->size;
    }

    pub func next(self) -> vec_iter<T> {
        if (self->_index >= self->vec->size) {
            return vec_iter<T> {
                vec: self->vec,
                _index: self->_index
            };
        }
        return vec_iter<T> {
            vec: self->vec,
            _index: self->_index + 1
        };
    }

    pub func get(self) -> T* {
        return self->vec->get(self->_index);
    }

    pub func index(self) -> u64 {
        return self->_index;
    }
}

impl primitive_vec<T> {
    pub func new() -> primitive_vec<T>* {
        var res = primitive_vec<T>::__alloc__();
        if (res == nil) {
            panic("primitive_vec<T>::new: failed to allocate memory");
        }
        res->size = 0;
        res->capacity = 4;
        res->data = malloc(res->capacity * T::__size__()) => T*;
        return res;
    }

    pub func instance() -> primitive_vec<T> {
        var res = primitive_vec<T> {
            size: 0,
            capacity: 4,
            data: nil
        };
        res.data = malloc(res.capacity * T::__size__()) => T*;
        return res;
    }

    pub func copy(self) -> primitive_vec<T>* {
        var res = primitive_vec<T>::new();
        for (var i: u64 = 0; i < self->size; i += 1) {
            res->push(self->data[i]);
        }
        return res;
    }

    pub func copy_instance(self) -> primitive_vec<T> {
        var res = primitive_vec<T>::instance();
        for (var i: u64 = 0; i < self->size; i += 1) {
            res.push(self->data[i]);
        }
        return res;
    }

    pub func clear(self) {
        self->size = 0;
    }

    pub func delete(self) {
        free(self->data => i8*);
        self->data = nil;
        self->size = 0;
        self->capacity = 0;
    }

    func extend_capacity(self) {
        self->capacity *= 2;
        self->data = realloc(self->data => i8*, self->capacity * T::__size__()) => T*;
    }

    pub func push(self, item: T) {
        if (self->size >= self->capacity) {
            self->extend_capacity();
        }
        self->data[self->size] = item;
        self->size += 1;
    }

    pub func get(self, index: u64) -> T {
        return self->data[index];
    }

    pub func back(self) -> T {
        if (self->size == 0) {
            panic("std::primitive_vec::back: empty vector");
        }
        return self->data[self->size - 1];
    }

    pub func pop_back(self) {
        if (self->size == 0) {
            panic("std::primitive_vec::pop_back: empty vector");
        }
        self->size -= 1;
    }

    pub func iter(self) -> primitive_vec_iter<T> {
        return primitive_vec_iter<T>::instance(self);
    }
}

struct primitive_vec_iter<T> {
    vec: primitive_vec<T>*,
    _index: u64
}

impl primitive_vec_iter<T> {
    pub func instance(v: primitive_vec<T>*) -> primitive_vec_iter<T> {
        return primitive_vec_iter<T> {
            vec: v,
            _index: 0
        };
    }

    pub func is_end(self) -> bool {
        return self->_index >= self->vec->size;
    }

    pub func next(self) -> primitive_vec_iter<T> {
        if (self->_index >= self->vec->size) {
            return primitive_vec_iter<T> {
                vec: self->vec,
                _index: self->_index
            };
        }
        return primitive_vec_iter<T> {
            vec: self->vec,
            _index: self->_index + 1
        };
    }

    pub func get(self) -> T {
        return self->vec->get(self->_index);
    }

    pub func index(self) -> u64 {
        return self->_index;
    }
}