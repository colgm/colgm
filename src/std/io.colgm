use std::libc::{ open, read, write, close, strlen, itoa, utoa, gcvt, free };
use std::str::{ str };
use err::panic::{ panic };

#[enable_if(target_os="macos")]
pub enum flag {
    O_RDONLY = 0x000,
    O_WRONLY = 0x001,
    O_RDWR   = 0x002,
    O_APPEND = 0x008,
    O_CREAT  = 0x200,
    O_TRUNC  = 0x400,
    O_EXCL   = 0x800
}

#[enable_if(target_os="linux")]
pub enum flag {
    O_RDONLY = 0x000,
    O_WRONLY = 0x001,
    O_RDWR   = 0x002,
    O_CREAT  = 0x040,
    O_EXCL   = 0x080,
    O_TRUNC  = 0x200,
    O_APPEND = 0x400
}

#[enable_if(target_os="windows")] // windows mingw-w64
pub enum flag {
    O_RDONLY = 0x0000,
    O_WRONLY = 0x0001,
    O_RDWR   = 0x0002,
    O_APPEND = 0x0008,
    O_CREAT  = 0x0100,
    O_TRUNC  = 0x0200,
    O_EXCL   = 0x0400,
    O_TEXT   = 0x4000,
    O_BINARY = 0x8000
}

pub func readfile_into_string(filename: i8*, dst: str*) -> i64 {
    var fd = open(filename, flag::O_RDONLY => i32, 0);
    if (fd < 0) {
        return fd => i64;
    }

    var buff = [i8; 4096];
    var readcount = read(fd, buff, 4095);
    buff[readcount] = 0;
    dst->append_i8_vec(buff);

    while (readcount != 0) {
        readcount = read(fd, buff, 4095);
        buff[readcount] = 0;
        dst->append_i8_vec(buff);
    }

    close(fd);
    return 0;
}

pub struct io {
    fd: i32,
    color_out: bool,
    cache: str*
}

impl io {
    pub func stdin() -> io {
        return io { fd: 0, color_out: false, cache: nil };
    }

    pub func stdout() -> io {
        return io { fd: 1, color_out: true, cache: nil };
    }

    pub func stderr() -> io {
        return io { fd: 2, color_out: true, cache: nil };
    }

    pub func disable_color(self) -> io {
        self->color_out = false;
        return self[0];
    }

    func force_flush(self) {
        write(self->fd, self->cache->c_str, self->cache->size => i64);
        self->cache->clear();
    }

    func check_and_flush(self) {
        if (self->cache == nil) {
            return;
        }
        // 8k threshold
        if (self->cache->size < 8192) {
            return;
        }
        self->force_flush();
    }

    pub func out(self, info: i8*) -> io {
        // do nothing if is stdin
        if (self->fd == 0) {
            return self[0];
        }

        if (self->cache == nil) {
            write(self->fd, info, strlen(info));
        } else {
            self->cache->append_i8_vec(info);
            self->check_and_flush();
        }
        return self[0];
    }

    pub func out_ch(self, info: i8) -> io {
        // do nothing if is stdin
        if (self->fd == 0) {
            return self[0];
        }

        if (self->cache == nil) {
            var buff = [i8; 4];
            buff[0] = info;
            write(self->fd, buff, 1);
        } else {
            self->cache->append_char(info);
            self->check_and_flush();
        }
        return self[0];
    }

    pub func out_i64(self, info: i64) -> io {
        // do nothing if is stdin
        if (self->fd == 0) {
            return self[0];
        }

        var buff = [i8; 256];
        itoa(info, buff, 10);
        if (self->cache == nil) {
            write(self->fd, buff, strlen(buff));
        } else {
            self->cache->append_i8_vec(buff);
            self->check_and_flush();
        }
        return self[0];
    }

    pub func out_u64(self, info: u64) -> io {
        // do nothing if is stdin
        if (self->fd == 0) {
            return self[0];
        }

        var buff = [i8; 256];
        utoa(info, buff, 10);
        if (self->cache == nil) {
            write(self->fd, buff, strlen(buff));
        } else {
            self->cache->append_i8_vec(buff);
            self->check_and_flush();
        }
        return self[0];
    }

    pub func out_f64(self, info: f64) -> io {
        // do nothing if is stdin
        if (self->fd == 0) {
            return self[0];
        }

        var buff = [i8; 32];
        gcvt(info, 6, buff);
        if (self->cache == nil) {
            write(self->fd, buff, strlen(buff));
        } else {
            self->cache->append_i8_vec(buff);
            self->check_and_flush();
        }
        return self[0];
    }

    pub func out_hex(self, info: i64) -> io {
        // do nothing if is stdin
        if (self->fd == 0) {
            return self[0];
        }

        var buff = [i8; 256];
        itoa(info, buff, 16);
        if (self->cache == nil) {
            write(self->fd, buff, strlen(buff));
        } else {
            self->cache->append_i8_vec(buff);
            self->check_and_flush();
        }
        return self[0];
    }

    pub func endln(self) {
        if (self->fd != 0) {
            if (self->cache == nil) {
                write(self->fd, "\n", 1);
            } else {
                self->cache->append_char('\n');
                self->check_and_flush();
            }
        }
    }
}

// ANSI escape sequence color
impl io {
    func color_out_core(self, color_code: i64, light: bool) -> io {
        if (!self->color_out) {
            return self[0];
        }
        var sequence = str::from("\e[");
        sequence.append_i64(color_code);
        if (!light) {
            sequence.append_i8_vec(";1");
        }
        sequence.append_i8_vec("m");
        self->out(sequence.c_str);
        sequence.delete();
        return self[0];
    }

    pub func red(self) -> io {
        return self->color_out_core(91, false);
    }

    pub func light_red(self) -> io {
        return self->color_out_core(91, true);
    }

    pub func green(self) -> io {
        return self->color_out_core(92, false);
    }

    pub func light_green(self) -> io {
        return self->color_out_core(92, true);
    }

    pub func orange(self) -> io {
        return self->color_out_core(93, false);
    }

    pub func light_orange(self) -> io {
        return self->color_out_core(93, true);
    }

    pub func blue(self) -> io {
        return self->color_out_core(94, false);
    }

    pub func light_blue(self) -> io {
        return self->color_out_core(94, true);
    }

    pub func purple(self) -> io {
        return self->color_out_core(95, false);
    }

    pub func light_purple(self) -> io {
        return self->color_out_core(95, true);
    }

    pub func cyan(self) -> io {
        return self->color_out_core(96, false);
    }

    pub func light_cyan(self) -> io {
        return self->color_out_core(96, true);
    }

    pub func white(self) -> io {
        return self->color_out_core(97, false);
    }

    pub func light_white(self) -> io {
        return self->color_out_core(97, true);
    }

    pub func reset(self) -> io {
        return self->color_out_core(0, true);
    }
}

impl io {
    func open_append_write_file(file: i8*) -> i32 {
        var flags = flag::O_WRONLY | flag::O_APPEND | flag::O_CREAT;
        var fd = open(file, flags => i32, 0o666); // rw-rw-rw-
        if (fd < 0) {
            panic("failed to open the file in append mode");
        }
        return fd;
    }

    func open_file(file: i8*) -> i32 {
        var flags = flag::O_WRONLY | flag::O_CREAT | flag::O_TRUNC;
        var fd = open(file, flags => i32, 0o666); // rw-rw-rw-
        if (fd < 0) {
            panic("failed to open the file in trunc mode");
        }
        return fd;
    }

    func logger(file: i8*) -> io {
        return io {
            fd: io::open_append_write_file(file),
            color_out: false,
            cache: str::new()
        };
    }

    pub func colgm_report() -> io {
        return io::logger("colgm.report.log");
    }

    pub func fileout(file: i8*) -> io {
        return io {
            fd: io::open_file(file),
            color_out: false,
            cache: str::new()
        };
    }

    pub func close(self) {
        if (self->fd <= 0 || self->fd == 1 || self->fd == 2) {
            return;
        }
        if (self->cache != nil) {
            self->force_flush();
            self->cache->delete();
            free(self->cache => i8*);
            self->cache = nil;
        }
        close(self->fd);
    }
}