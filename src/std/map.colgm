use std::str::{ str };
use std::libc::{ malloc, free };
use std::list::{ list, list_iter };
use std::io::{ io };

use err::panic::{ panic };

pub struct pair<K, V> {
    key: K,
    value: V
}

impl pair<K, V> {
    pub func instance(key: K*, value: V*) -> pair<K, V> {
        return pair<K, V> {
            key: key->copy_instance(),
            value: value->copy_instance()
        };
    }

    pub func delete(self) {
        self->key.delete();
        self->value.delete();
    }

    pub func copy(self) -> pair<K, V>* {
        var res = pair<K, V>::__alloc__();
        if (res == nil) {
            panic("failed to allocate memory");
        }
        res->key = self->key.copy_instance();
        res->value = self->value.copy_instance();
        return res;
    }

    pub func copy_instance(self) -> pair<K, V> {
        return pair<K, V> {
            key: self->key.copy_instance(),
            value: self->value.copy_instance()
        };
    }
}

pub struct hashmap<K, V> {
    size: u64,
    bucket_capacity: u64,
    bucket: list<pair<K, V>>*
}

impl hashmap<K, V> {
    pub func new() -> hashmap<K, V>* {
        var res = hashmap<K, V>::__alloc__();
        if (res == nil) {
            panic("failed to allocate memory");
        }
        res->init();
        return res;
    }

    pub func instance() -> hashmap<K, V> {
        var res = hashmap<K, V> {};
        res.init();
        return res;
    }

    pub func copy(self) -> hashmap<K, V>* {
        var res = hashmap<K, V>::new();
        foreach (var i; self) {
            res->insert(i.key(), i.value());
        }
        return res;
    }

    pub func copy_instance(self) -> hashmap<K, V> {
        var res = hashmap<K, V>::instance();
        foreach (var i; self) {
            res.insert(i.key(), i.value());
        }
        return res;
    }

    func init(self) {
        self->size = 0;
        self->bucket_capacity = 512;
        self->bucket = malloc(self->bucket_capacity
                              * list<pair<K, V>>::__size__())
                              => list<pair<K, V>>*;
        for (var i: u64 = 0; i < self->bucket_capacity; i += 1) {
            self->bucket[i] = list<pair<K, V>>::instance();
        }
    }

    pub func clear(self) {
        for (var i: u64 = 0; i < self->bucket_capacity; i += 1) {
            self->bucket[i].clear();
        }
        self->size = 0;
    }

    pub func delete(self) {
        for (var i: u64 = 0; i < self->bucket_capacity; i += 1) {
            self->bucket[i].delete();
        }
        free(self->bucket => i8*);
        self->size = 0;
        self->bucket_capacity = 0;
        self->bucket = nil;
    }
}

impl hashmap<K, V> {
    pub func has(self, key: K*) -> bool {
        if (self->bucket_capacity == 0) {
            panic("capacity is 0");
        }
        var hash = key->hash() % self->bucket_capacity;
        var bucket = self->bucket[hash].__ptr__();
        foreach (var i; bucket) {
            if (i.elem()->key.eq(key)) {
                return true;
            }
        }
        return false;
    }

    pub func get(self, key: K*) -> V* {
        if (self->bucket_capacity == 0) {
            panic("capacity is 0");
        }
        var hash = key->hash() % self->bucket_capacity;
        var bucket = self->bucket[hash].__ptr__();
        foreach (var i; bucket) {
            if (i.elem()->key.eq(key)) {
                return i.elem()->value.__ptr__();
            }
        }
        return nil;
    }

    pub func insert(self, key: K*, value: V*) {
        var hash = key->hash() % self->bucket_capacity;
        var bucket = self->bucket[hash].__ptr__();
        foreach (var i; bucket) {
            if (i.elem()->key.eq(key)) {
                var tmp = i.elem();
                tmp->value.delete();
                tmp->value = value->copy_instance();
                return;
            }
        }
        
        var tmp_pair = pair<K, V>::instance(key, value);
        // do copy in insert method
        bucket->insert(tmp_pair.__ptr__());
        tmp_pair.delete();
        self->size += 1;
    }

    pub func empty(self) -> bool {
        return self->size == 0;
    }
}

struct iter<K, V> {
    map: hashmap<K, V>*,
    bucket_index: u64,
    bucket_iter: list_iter<pair<K, V>>
}

impl iter<K, V> {
    func instance(map: hashmap<K, V>*) -> iter<K, V> {
        var res = iter<K, V> {
            map: map,
            bucket_index: 0,
            bucket_iter: map->bucket[0].iter()
        };
        while (res.bucket_iter.is_end()) {
            res.bucket_index += 1;
            if (res.bucket_index >= map->bucket_capacity) {
                return res;
            }
            res.bucket_iter = map->bucket[res.bucket_index].iter();
        }
        return res;
    }

    pub func next(self) -> iter<K, V> {
        if (!self->bucket_iter.is_end()) {
            self->bucket_iter = self->bucket_iter.next();
        }

        while (self->bucket_iter.is_end()) {
            self->bucket_index += 1;
            if (self->bucket_index >= self->map->bucket_capacity) {
                break;
            }
            self->bucket_iter = self->map->bucket[self->bucket_index].iter();
        }

        return iter<K, V> {
            map: self->map,
            bucket_index: self->bucket_index,
            bucket_iter: self->bucket_iter,
        };
    }

    pub func is_end(self) -> bool {
        return self->bucket_index >= self->map->bucket_capacity &&
               self->bucket_iter.is_end();
    }

    pub func key(self) -> K* {
        return self->bucket_iter.elem()->key.__ptr__();
    }

    pub func value(self) -> V* {
        return self->bucket_iter.elem()->value.__ptr__();
    }
}

impl hashmap<K, V> {
    pub func iter(self) -> iter<K, V> {
        return iter<K, V>::instance(self);
    }
}