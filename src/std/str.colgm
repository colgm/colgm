use std::libc::{ malloc, realloc, free, memcpy, strlen, streq, exit, write };

pub struct str {
    c_str: i8*,
    size: u64,
    capacity: u64
}

impl str {
    pub func new() -> str* {
        var res = str::__alloc__();
        res->c_str = nil;
        res->init();
        return res;
    }

    pub func instance() -> str {
        var res = str {};
        res.init();
        return res;
    }

    pub func init(self) {
        self->c_str = malloc(16 => u64);
        self->c_str[0] = '\0';

        self->size = 0 => u64;
        self->capacity = 16 => u64;
    }

    pub func copy_instance(self) -> str {
        var new_str = str {
            c_str: malloc(self->capacity),
            size: self->size,
            capacity: self->capacity
        };

        memcpy(new_str.c_str, self->c_str, self->size);
        new_str.c_str[self->size] = '\0';

        return new_str;
    }

    pub func delete(self) {
        free(self->c_str);
        
        self->c_str = nil;
        self->size = 0 => u64;
        self->capacity = 0 => u64;
    }
}

impl str {
    pub func empty(self) -> bool {
        return self->size == (0 => u64);
    }

    pub func clear(self) {
        self->c_str[0] = '\0';
        self->size = 0 => u64;
    }
}

impl str {
    pub func copy_const(self, src: i8*) {
        var len = strlen(src) => u64;

        // re-allocate memory
        if (len >= self->size) {
            free(self->c_str);
            self->c_str = malloc(len + (1 => u64));
            self->size = len + 1 => u64;
            self->capacity = len + 1 => u64;
        }

        memcpy(self->c_str, src, len);
        self->c_str[len] = '\0';
        self->size = len;
    }

    func expand_capacity(self) {
        self->capacity *= 2 => u64;
        self->c_str = realloc(self->c_str, self->capacity);
    }

    pub func append_char(self, ch: i8) -> str* {
        // self->size == self->capacity will cause memory out of bounds error
        // but only crashes on macOS aarch64
        // crash happens in this case:
        //
        // | str 1 capacity 8 | str 2 capacity 8 |
        //                   -^- continuous allocated memory block
        // |  str 1 |  str 2 |
        // |+++++++0|        |
        //         ^ size = 7 (real 8), capacity = 8
        //
        // if expands when size + 1 == capacity
        // |  str 1         |  str 2 |
        // |++++++++0       |        |
        //          ^ size = 8 (real 9), capacity = 16
        //
        // if not
        // |  str 1 |  str 2 |
        // |++++++++0        |
        //          ^ size = 8 (real 9), capacity = 8
        //          ^ this breaks the memory block of str2
        //
        // so after str2 is freed, the memory block head is still broken
        // and then we use malloc to allocate this memory block again
        // and free again, the memory block will cause
        // "free(): invalid pointer"
        // "malloc: *** error for object 0x.....: pointer being freed was not allocated"
        //
        // that's wired T^T
        if (self->size + (1 => u64) == self->capacity) {
            self->expand_capacity();
        }

        self->c_str[self->size] = ch;
        self->c_str[self->size + (1 => u64)] = '\0';
        self->size += 1 => u64;
        return self;
    }

    pub func append_str(self, src: str*) -> str* {
        var len = src->size => i64;
        for (var index = 0; index < len; index += 1) {
            self->append_char(src->c_str[index]);
        }
        return self;
    }

    pub func append_i8_vec(self, src: i8*) -> str* {
        var len = strlen(src);
        for (var index = 0; index < len; index += 1) {
            self->append_char(src[index]);
        }
        return self;
    }

    pub func pop(self) {
        if (self->size > 0 => u64) {
            self->size -= 1 => u64;
            self->c_str[self->size] = '\0';
        }
    }
}

impl str {
    pub func get(self, index: u64) -> i8 {
        if (index >= self->size) {
            var report_info = "std::str::get: index out of range\n\n";
            write(2 => i32, report_info, strlen(report_info));
            exit(-1 => i32);
        }
        return self->c_str[index];
    }

    pub func eq_const(self, src: i8*) -> bool {
        return streq(self->c_str, src);
    }

    pub func eq_str(self, src: str) -> bool {
        return streq(self->c_str, src.c_str);
    }

    pub func eq(self, src: str*) -> bool {
        return streq(self->c_str, src->c_str);
    }
}