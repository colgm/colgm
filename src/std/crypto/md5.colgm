use std::str::{ str };
use std::libc::{ utoa, malloc, realloc, free };

struct md5_array<T> {
    data: T*,
    size: u64
}

impl md5_array<T> {
    pub func instance(len: u64) -> md5_array<T> {
        return md5_array<T>{
            data: malloc(len * T::__size__()) => T*,
            size: len
        };
    }

    pub func delete(self) {
        free(self.data => i8*);
    }

    pub func resize(self, len: u64) {
        self.data = realloc(self.data => i8*, len * T::__size__()) => T*;
        self.size = len;
    }
}

func lshift(num: u32, cx: u64) -> u32 {
    for (var i: u64 = 0; i < cx; i += 1) {
        num *= 2;
    }
    return num;
}

func rshift(num: u32, cx: u64) -> u32 {
    for (var i: u64 = 0; i < cx; i += 1) {
        num /= 2;
    }
    return num;
}

func rol(num: u32, cx: u64) -> u32 {
    return lshift(num, cx) | rshift(num, 32 => u64 - cx);
}

func F(x: u32, y: u32, z: u32) -> u32 {
    return (x & y) | ((~x) & z);
}

func G(x: u32, y: u32, z: u32) -> u32 {
    return (x & z) | (y & (~z));
}

func H(x: u32, y: u32, z: u32) -> u32 {
    return x ^ y ^ z;
}

func I(x: u32, y: u32, z: u32) -> u32 {
    return y ^ (x | (~z));
}

func hex32str(A: u32, B: u32, C: u32, D: u32) -> str {
    var buff: [i8; 16] = [];
    var res = str::instance();

    for (var i: u64 = 0; i < 4; i += 1) {
        utoa((A & 0xff) => u64, buff, 16);
        if ((A & 0xff) < 0x10) {
            res.append("0");
        }
        res.append(buff);
        A /= 256;
    }

    for (var i: u64 = 0; i < 4; i += 1) {
        utoa((B & 0xff) => u64, buff, 16);
        if ((B & 0xff) < 0x10) {
            res.append("0");
        }
        res.append(buff);
        B /= 256;
    }

    for (var i: u64 = 0; i < 4; i += 1) {
        utoa((C & 0xff) => u64, buff, 16);
        if ((C & 0xff) < 0x10) {
            res.append("0");
        }
        res.append(buff);
        C /= 256;
    }

    for (var i: u64 = 0; i < 4; i += 1) {
        utoa((D & 0xff) => u64, buff, 16);
        if ((D & 0xff) < 0x10) {
            res.append("0");
        }
        res.append(buff);
        D /= 256;
    }

    return res;
}

pub func md5(input: str&) -> str {
    var K = [
        0xd76aa478 => u32, 0xe8c7b756, 0x242070db, 0xc1bdceee, 0xf57c0faf, 0x4787c62a, 0xa8304613, 0xfd469501,
        0x698098d8, 0x8b44f7af, 0xffff5bb1, 0x895cd7be, 0x6b901122, 0xfd987193, 0xa679438e, 0x49b40821,
        0xf61e2562, 0xc040b340, 0x265e5a51, 0xe9b6c7aa, 0xd62f105d, 0x02441453, 0xd8a1e681, 0xe7d3fbc8,
        0x21e1cde6, 0xc33707d6, 0xf4d50d87, 0x455a14ed, 0xa9e3e905, 0xfcefa3f8, 0x676f02d9, 0x8d2a4c8a,
        0xfffa3942, 0x8771f681, 0x6d9d6122, 0xfde5380c, 0xa4beea44, 0x4bdecfa9, 0xf6bb4b60, 0xbebfbc70,
        0x289b7ec6, 0xeaa127fa, 0xd4ef3085, 0x04881d05, 0xd9d4d039, 0xe6db99e5, 0x1fa27cf8, 0xc4ac5665,
        0xf4292244, 0x432aff97, 0xab9423a7, 0xfc93a039, 0x655b59c3, 0x8f0ccc92, 0xffeff47d, 0x85845dd1,
        0x6fa87e4f, 0xfe2ce6e0, 0xa3014314, 0x4e0811a1, 0xf7537e82, 0xbd3af235, 0x2ad7d2bb, 0xeb86d391
    ];

    var S = [
        7 => u64, 12, 17, 22, 7, 12, 17, 22, 7, 12, 17, 22, 7, 12, 17, 22,
        5, 9,  14, 20, 5, 9,  14, 20, 5, 9,  14, 20, 5, 9,  14, 20,
        4, 11, 16, 23, 4, 11, 16, 23, 4, 11, 16, 23, 4, 11, 16, 23,
        6, 10, 15, 21, 6, 10, 15, 21, 6, 10, 15, 21, 6, 10, 15, 21
    ];

    var idx = [
        0 => u64, 1,  2,  3,  4,  5,  6,  7,  8, 9,  10, 11, 12, 13, 14, 15,
        1, 6, 11, 0,  5,  10, 15, 4,  9,  14, 3, 8,  13, 2,  7,  12,
        5, 8, 11, 14, 1,  4,  7,  10, 13, 0,  3, 6,  9,  12, 15, 2,
        0, 7, 14, 5,  12, 3,  10, 1,  8,  15, 6, 13, 4,  11, 2,  9
    ];

    var s_size = input.size;
    var len = s_size * 8;
    var res = md5_array<u8>::instance(s_size);
    defer res.delete();
    for (var i: u64 = 0; i < s_size; i += 1) {
        res.data[i] = input.c_str[i] => u8;
    }

    // +------len------+--1~512--+--64--+
    // |      text     |  fill   | size |
    // +---------------+---------+------+ N*512 bit
    var mod = s_size % 64;
    var res_size: u64 = 0;
    if (mod == 56) {
        res_size = s_size + 64;
    } elsif (mod < 56) {
        res_size = (s_size ^ (s_size & 63)) + 56;
    } elsif (mod > 56) {
        res_size = (s_size ^ (s_size & 63)) + 120; // 512 + 448 = 960 960 / 8 = 120
    }
    res.resize(res_size);   
    res.data[s_size] = 0x80;
    for (var i = s_size + 1; i < res_size; i += 1) {
        res.data[i] = 0;
    }

    // little endian
    res.resize(res_size + 8);
    s_size = res.size;
    var lower = (len & 0xffffffff) => u32;
    var higher = (len / 0x100000000) => u32;
    for (var i: u64 = 4; i > 0; i -= 1) {
        res.data[s_size - 4 - i] = (lower & 0xff) => u8;
        lower /= 256;
    }
    for (var i: u64 = 4; i > 0; i -= 1) {
        res.data[s_size - i] = (higher & 0xff) => u8;
        higher /= 256;
    }

    var tmp = md5_array<u32>::instance(res.size / 4);
    defer tmp.delete();
    for (var i: u64 = 0; i < res.size; i += 4) {
        tmp.data[i / 4]  = lshift(res.data[i + 3] => u32, 24);
        tmp.data[i / 4] += lshift(res.data[i + 2] => u32, 16);
        tmp.data[i / 4] += lshift(res.data[i + 1] => u32, 8);
        tmp.data[i / 4] += res.data[i] => u32;
    }

    var A: u32 = 0x67452301;
    var B: u32 = 0xefcdab89;
    var C: u32 = 0x98badcfe;
    var D: u32 = 0x10325476;

    for (var i: u64 = 0; i < tmp.size; i += 16) {
        var a = A;
        var b = B;
        var c = C;
        var d = D;

        for (var j: u64 = 0; j < 16; j += 1) {
            var f = F(b, c, d);
            var t = rol(a + f + K[j] + tmp.data[i + idx[j]], S[j]);
            a = d;
            d = c;
            c = b;
            b += t;
        }

         for (var j: u64 = 0; j < 16; j += 1) {
            var f = G(b, c, d);
            var t = rol(a + f + K[j + 16] + tmp.data[i + idx[j + 16]], S[j + 16]);
            a = d;
            d = c;
            c = b;
            b += t;
        }

         for (var j: u64 = 0; j < 16; j += 1) {
            var f = H(b, c, d);
            var t = rol(a + f + K[j + 32] + tmp.data[i + idx[j + 32]], S[j + 32]);
            a = d;
            d = c;
            c = b;
            b += t;
        }

         for (var j: u64 = 0; j < 16; j += 1) {
            var f = I(b, c, d);
            var t = rol(a + f + K[j + 48] + tmp.data[i + idx[j + 48]], S[j + 48]);
            a = d;
            d = c;
            c = b;
            b += t;
        }

        A += a;
        B += b;
        C += c;
        D += d;
    }

    return hex32str(A, B, C, D);
}