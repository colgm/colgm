use std::io::{ io };
use std::str::{ str };
use std::vec::{ vec };
use std::libc::{ free };

use err::panic::{ unimplemented };

pub enum DI_kind {
    DI_error, // reserved for errors
    DI_null,
    DI_named_metadata,
    DI_ref_index,
    DI_list,
    DI_i32,
    DI_string,
    DI_file,
    DI_compile_unit,
    DI_basic_type,
    DI_structure_type,
    DI_union_type,
    DI_enum_type,
    DI_enumerator,
    DI_subprogram,
    DI_subroutine,
    DI_location,
    DW_TAG_pointer_type,
    DW_TAG_member
}

pub func DI_ERROR_INDEX() -> u64 {
    return 0xffffffffffffffff;
}

pub struct DI_node {
    kind: DI_kind,
    index: u64
}

impl DI_node {
    pub func instance(k: DI_kind, i: u64) -> DI_node {
        return DI_node {
            kind: k,
            index: i
        };
    }

    pub func delete(self) {
        match (self->kind) {
            DI_kind::DI_null => {}
            DI_kind::DI_named_metadata => {
                var n = self => DI_named_metadata*;
                n->delete();
            }
            DI_kind::DI_ref_index => {}
            DI_kind::DI_list => {
                var n = self => DI_list*;
                n->delete();
            }
            DI_kind::DI_i32 => {}
            DI_kind::DI_string => {
                var n = self => DI_string*;
                n->delete();
            }
            DI_kind::DI_file => {
                var n = self => DI_file*;
                n->delete();
            }
            DI_kind::DI_compile_unit => {
                var n = self => DI_compile_unit*;
                n->delete();
            }
            DI_kind::DI_basic_type => {
                var n = self => DI_basic_type*;
                n->delete();
            }
            DI_kind::DI_structure_type => {
                var n = self => DI_structure_type*;
                n->delete();
            }
            DI_kind::DI_union_type => {
                var n = self => DI_union_type*;
                n->delete();
            }
            DI_kind::DI_enum_type => {
                var n = self => DI_enum_type*;
                n->delete();
            }
            DI_kind::DI_enumerator => {
                var n = self => DI_enumerator*;
                n->delete();
            }
            DI_kind::DI_subprogram => {
                var n = self => DI_subprogram*;
                n->delete();
            }
            DI_kind::DI_subroutine => {}
            DI_kind::DI_location => {
                var n = self => DI_location*;
                n->delete();
            }
            DI_kind::DW_TAG_pointer_type => {
                var n = self => DW_TAG_pointer_type*;
                n->delete();
            }
            DI_kind::DW_TAG_member => {
                var n = self => DW_TAG_member*;
                n->delete();
            }
            _ => unimplemented();
        }
    }

    pub func dump_index(self, out: io) {
        if (self->index != DI_ERROR_INDEX()) {
            out.out("!").out_u64(self->index).out(" = ");
        }
    }

    pub func dump(self, out: io) {
        self->dump_index(out);
        match (self->kind) {
            DI_kind::DI_null => {
                var n = self => DI_null*;
                n->dump(out);
            }
            DI_kind::DI_named_metadata => {
                var n = self => DI_named_metadata*;
                n->dump(out);
            }
            DI_kind::DI_ref_index => {
                var n = self => DI_ref_index*;
                n->dump(out);
            }
            DI_kind::DI_list => {
                var n = self => DI_list*;
                n->dump(out);
            }
            DI_kind::DI_i32 => {
                var n = self => DI_i32*;
                n->dump(out);
            }
            DI_kind::DI_string => {
                var n = self => DI_string*;
                n->dump(out);
            }
            DI_kind::DI_file => {
                var n = self => DI_file*;
                n->dump(out);
            }
            DI_kind::DI_compile_unit => {
                var n = self => DI_compile_unit*;
                n->dump(out);
            }
            DI_kind::DI_basic_type => {
                var n = self => DI_basic_type*;
                n->dump(out);
            }
            DI_kind::DI_structure_type => {
                var n = self => DI_structure_type*;
                n->dump(out);
            }
            DI_kind::DI_union_type => {
                var n = self => DI_union_type*;
                n->dump(out);
            }
            DI_kind::DI_enum_type => {
                var n = self => DI_enum_type*;
                n->dump(out);
            }
            DI_kind::DI_enumerator => {
                var n = self => DI_enumerator*;
                n->dump(out);
            }
            DI_kind::DI_subprogram => {
                var n = self => DI_subprogram*;
                n->dump(out);
            }
            DI_kind::DI_subroutine => {
                var n = self => DI_subroutine*;
                n->dump(out);
            }
            DI_kind::DI_location => {
                var n = self => DI_location*;
                n->dump(out);
            }
            DI_kind::DW_TAG_pointer_type => {
                var n = self => DW_TAG_pointer_type*;
                n->dump(out);
            }
            DI_kind::DW_TAG_member => {
                var n = self => DW_TAG_member*;
                n->dump(out);
            }
            _ => unimplemented();
        }
    }
}

pub struct DI_null {
    base: DI_node
}

impl DI_null {
    pub func new() -> DI_null* {
        var res = DI_null::__alloc__();
        res->base = DI_node::instance(DI_kind::DI_null, DI_ERROR_INDEX());
        return res;
    }

    pub func dump(self, out: io) {
        out.out("null");
    }
}

pub struct DI_named_metadata {
    base: DI_node,
    name: str,
    nodes: vec<DI_node*>
}

impl DI_named_metadata {
    pub func new(name: i8*) -> DI_named_metadata* {
        var res = DI_named_metadata::__alloc__();
        res->base = DI_node::instance(DI_kind::DI_named_metadata, DI_ERROR_INDEX());
        res->name = str::from(name);
        res->nodes = vec<DI_node*>::instance();
        return res;
    }

    pub func delete(self) {
        self->name.delete();
        foreach (var i; self->nodes) {
            var n = i.get();
            n->delete();
            free(n => i8*);
        }
        self->nodes.delete();
    }

    pub func add(self, n: DI_node*) {
        self->nodes.push(n);
    }

    pub func dump(self, out: io) {
        out.out("!").out(self->name.c_str).out(" = !{");
        foreach (var i; self->nodes) {
            i.get()->dump(out);
            if (i.index() != self->nodes.size - 1) {
                out.out(", ");
            }
        }
        out.out("}");
    }
}

pub struct DI_ref_index {
    base: DI_node,
    index: u64
}

impl DI_ref_index {
    pub func new(index: u64) -> DI_ref_index* {
        var res = DI_ref_index::__alloc__();
        res->base = DI_node::instance(DI_kind::DI_ref_index, DI_ERROR_INDEX());
        res->index = index;
        return res;
    }

    pub func dump(self, out: io) {
        out.out("!").out_u64(self->index);
    }
}

pub struct DI_list {
    base: DI_node,
    nodes: vec<DI_node*>
}

impl DI_list {
    pub func new(i: u64) -> DI_list* {
        var res = DI_list::__alloc__();
        res->base = DI_node::instance(DI_kind::DI_list, i);
        res->nodes = vec<DI_node*>::instance();
        return res;
    }

    pub func delete(self) {
        foreach (var i; self->nodes) {
            var n = i.get();
            n->delete();
            free(n => i8*);
        }
        self->nodes.delete();
    }

    pub func add(self, n: DI_node*) {
        self->nodes.push(n);
    }

    pub func dump(self, out: io) {
        out.out("!{");
        foreach (var i; self->nodes) {
            i.get()->dump(out);
            if (i.index() != self->nodes.size - 1) {
                out.out(", ");
            }
        }
        out.out("}");
    }
}

pub struct DI_i32 {
    base: DI_node,
    value: i32
}

impl DI_i32 {
    pub func new(value: i32) -> DI_i32* {
        var res = DI_i32::__alloc__();
        res->base = DI_node::instance(DI_kind::DI_i32, DI_ERROR_INDEX());
        res->value = value;
        return res;
    }

    pub func dump(self, out: io) {
        out.out("i32 ").out_i64(self->value => i64);
    }
}

pub struct DI_string {
    base: DI_node,
    value: str
}

impl DI_string {
    pub func new(value: i8*) -> DI_string* {
        var res = DI_string::__alloc__();
        res->base = DI_node::instance(DI_kind::DI_string, DI_ERROR_INDEX());
        res->value = str::from(value);
        return res;
    }

    pub func delete(self) {
        self->value.delete();
    }

    pub func dump(self, out: io) {
        out.out("!\"").out(self->value.c_str).out("\"");
    }
}

pub struct DI_file {
    base: DI_node,
    filename: str,
    directory: str
}

impl DI_file {
    pub func new(i: u64, f: i8*, d: i8*) -> DI_file* {
        var res = DI_file::__alloc__();
        res->base = DI_node::instance(DI_kind::DI_file, i);
        res->filename = str::from(f);
        res->directory = str::from(d);
        return res;
    }

    pub func delete(self) {
        self->filename.delete();
        self->directory.delete();
    }

    pub func dump(self, out: io) {
        out.out("!DIFile(filename: \"").out(self->filename.c_str);
        out.out("\", directory: \"").out(self->directory.c_str).out("\")");
    }
}

pub struct DI_compile_unit {
    base: DI_node,
    producer: str,
    file_index: u64,
    imports_index: u64,
    enums_index: u64
}

impl DI_compile_unit {
    pub func new(i: u64, p: i8*, fi: u64) -> DI_compile_unit* {
        var res = DI_compile_unit::__alloc__();
        res->base = DI_node::instance(DI_kind::DI_compile_unit, i);
        res->producer = str::from(p);
        res->file_index = fi;
        res->imports_index = DI_ERROR_INDEX();
        res->enums_index = DI_ERROR_INDEX();
        return res;
    }

    pub func delete(self) {
        self->producer.delete();
    }

    pub func dump(self, out: io) {
        out.out("distinct !DICompileUnit(");
        out.out("language: DW_LANG_C99, ");
        out.out("file: !").out_u64(self->file_index).out(", ");
        out.out("producer: \"").out(self->producer.c_str).out("\", ");
        if (self->imports_index != DI_ERROR_INDEX()) {
            out.out("imports: !").out_u64(self->imports_index).out(", ");
        }
        if (self->enums_index != DI_ERROR_INDEX()) {
            out.out("enums: !").out_u64(self->enums_index).out(", ");
        }
        out.out("isOptimized: false, runtimeVersion: 0, emissionKind: FullDebug)");
    }
}

pub struct DI_basic_type {
    base: DI_node,
    name: str,
    size_in_bits: u64,
    encoding: str
}

impl DI_basic_type {
    pub func new(i: u64, n: i8*, s: u64, e: i8*) -> DI_basic_type* {
        var res = DI_basic_type::__alloc__();
        res->base = DI_node::instance(DI_kind::DI_basic_type, i);
        res->name = str::from(n);
        res->size_in_bits = s;
        res->encoding = str::from(e);
        return res;
    }

    pub func delete(self) {
        self->name.delete();
        self->encoding.delete();
    }

    pub func dump(self, out: io) {
        out.out("!DIBasicType(name: \"").out(self->name.c_str);
        out.out("\", size: ").out_u64(self->size_in_bits).out(", encoding: ");
        out.out(self->encoding.c_str).out(")");
    }
}

pub struct DI_structure_type {
    base: DI_node,
    name: str,
    identifier: str,
    file_index: u64,
    line: i64,
    elements: u64
}

impl DI_structure_type {
    pub func new(i: u64, n: i8*, id: i8*, fi: u64, l: i64) -> DI_structure_type* {
        var res = DI_structure_type::__alloc__();
        res->base = DI_node::instance(DI_kind::DI_structure_type, i);
        res->name = str::from(n);
        res->identifier = str::from(id);
        res->file_index = fi;
        res->line = l;
        res->elements = DI_ERROR_INDEX();
        return res;
    }

    pub func delete(self) {
        self->name.delete();
        self->identifier.delete();
    }

    pub func dump(self, out: io) {
        out.out("!DICompositeType(tag: DW_TAG_structure_type, ");
        out.out("name: \"").out(self->name.c_str).out("\", ");
        out.out("file: !").out_u64(self->file_index).out(", ");
        out.out("line: ").out_i64(self->line).out(", ");
        out.out("identifier: \"").out(self->identifier.c_str).out("\"");
        if (self->elements != DI_ERROR_INDEX()) {
            out.out(", elements: !").out_u64(self->elements);
        }
        out.out(")");
    }
}

pub struct DI_union_type {
    base: DI_node,
    name: str,
    identifier: str,
    file_index: u64,
    line: i64,
    elements: u64
}

impl DI_union_type {
    pub func new(i: u64, n: i8*, id: i8*, fi: u64, l: i64) -> DI_union_type* {
        var res = DI_union_type::__alloc__();
        res->base = DI_node::instance(DI_kind::DI_union_type, i);
        res->name = str::from(n);
        res->identifier = str::from(id);
        res->file_index = fi;
        res->line = l;
        res->elements = DI_ERROR_INDEX();
        return res;
    }

    pub func delete(self) {
        self->name.delete();
        self->identifier.delete();
    }

    pub func dump(self, out: io) {
        out.out("!DICompositeType(tag: DW_TAG_union_type, ");
        out.out("name: \"").out(self->name.c_str).out("\", ");
        out.out("file: !").out_u64(self->file_index).out(", ");
        out.out("line: ").out_i64(self->line).out(", ");
        out.out("identifier: \"").out(self->identifier.c_str).out("\"");
        if (self->elements != DI_ERROR_INDEX()) {
            out.out(", elements: !").out_u64(self->elements);
        }
        out.out(")");
    }
}

pub struct DI_enum_type {
    base: DI_node,
    name: str,
    identifier: str,
    file_index: u64,
    line: i64,
    base_type_index: u64,
    elements_index: u64
}

impl DI_enum_type {
    pub func new(i: u64, n: i8*, id: i8*, fi: u64, l: i64, bti: u64) -> DI_enum_type* {
        var res = DI_enum_type::__alloc__();
        res->base = DI_node::instance(DI_kind::DI_enum_type, i);
        res->name = str::from(n);
        res->identifier = str::from(id);
        res->file_index = fi;
        res->line = l;
        res->base_type_index = bti;
        res->elements_index = DI_ERROR_INDEX();
        return res;
    }

    pub func delete(self) {
        self->name.delete();
        self->identifier.delete();
    }

    pub func dump(self, out: io) {
        out.out("!DICompositeType(tag: DW_TAG_enumeration_type, ");
        out.out("name: \"").out(self->name.c_str).out("\", ");
        out.out("file: !").out_u64(self->file_index).out(", ");
        out.out("line: ").out_i64(self->line).out(", ");
        out.out("baseType: !").out_u64(self->base_type_index).out(", ");
        if (self->elements_index != DI_ERROR_INDEX()) {
            out.out("elements: !").out_u64(self->elements_index).out(", ");
        }
        out.out("identifier: \"").out(self->identifier.c_str).out("\")");
    }
}

pub struct DI_enumerator {
    base: DI_node,
    name: str,
    value: i64
}

impl DI_enumerator {
    pub func new(i: u64, n: i8*, v: i64) -> DI_enumerator* {
        var res = DI_enumerator::__alloc__();
        res->base = DI_node::instance(DI_kind::DI_enumerator, i);
        res->name = str::from(n);
        res->value = v;
        return res;
    }

    pub func delete(self) {
        self->name.delete();
    }

    pub func dump(self, out: io) {
        out.out("!DIEnumerator(name: \"").out(self->name.c_str).out("\", ");
        out.out("value: ").out_i64(self->value).out(")");
    }
}

pub struct DI_subprogram {
    base: DI_node,
    name: str,
    file_index: u64,
    line: i64,
    type_index: u64,
    compile_unit_index: u64
}

impl DI_subprogram {
    pub func new(i: u64, n: i8*, fi: u64, l: i64, ti: u64, cu: u64) -> DI_subprogram* {
        var res = DI_subprogram::__alloc__();
        res->base = DI_node::instance(DI_kind::DI_subprogram, i);
        res->name = str::from(n);
        res->file_index = fi;
        res->line = l;
        res->type_index = ti;
        res->compile_unit_index = cu;
        return res;
    }

    pub func delete(self) {
        self->name.delete();
    }

    pub func dump(self, out: io) {
        out.out("distinct !DISubprogram(name: \"").out(self->name.c_str).out("\", ");
        out.out("file: !").out_u64(self->file_index).out(", ");
        out.out("line: ").out_i64(self->line).out(", ");
        out.out("type: !").out_u64(self->type_index).out(", ");
        out.out("unit: !").out_u64(self->compile_unit_index).out(")");
    }
}

pub struct DI_subroutine {
    base: DI_node,
    types_index: u64
}

impl DI_subroutine {
    pub func new(i: u64, ti: u64) -> DI_subroutine* {
        var res = DI_subroutine::__alloc__();
        res->base = DI_node::instance(DI_kind::DI_subroutine, i);
        res->types_index = ti;
        return res;
    }

    pub func dump(self, out: io) {
        out.out("!DISubroutineType(types: !").out_u64(self->types_index).out(")");
    }
}

pub struct DI_location {
    base: DI_node,
    file: str,
    line: i64,
    column: i64,
    scope_index: u64
}

impl DI_location {
    pub func new(i: u64, f: i8*, l: i64, c: i64, si: u64) -> DI_location* {
        var res = DI_location::__alloc__();
        res->base = DI_node::instance(DI_kind::DI_location, i);
        res->file = str::from(f);
        res->line = l;
        res->column = c;
        res->scope_index = si;
        return res;
    }

    pub func delete(self) {
        self->file.delete();
    }

    pub func get_index(self) -> u64 {
        return self->base.index;
    }

    pub func dump(self, out: io) {
        out.out("!DILocation(line: ").out_i64(self->line).out(", ");
        out.out("column: ").out_i64(self->column).out(", ");
        out.out("scope: !").out_u64(self->scope_index).out(")");
        if (!self->file.empty()) {
            out.out(" ; ").out(self->file.c_str);
        }
    }
}

pub struct DW_TAG_pointer_type {
    base: DI_node,
    base_type_index: u64,
    size: u64,
    name: str
}

impl DW_TAG_pointer_type {
    pub func new(i: u64, bti: u64, s: u64, n: i8*) -> DW_TAG_pointer_type* {
        var res = DW_TAG_pointer_type::__alloc__();
        res->base = DI_node::instance(DI_kind::DW_TAG_pointer_type, i);
        res->base_type_index = bti;
        res->size = s;
        res->name = str::from(n);
        return res;
    }

    pub func delete(self) {
        self->name.delete();
    }

    pub func dump(self, out: io) {
        out.out("!DIDerivedType(tag: DW_TAG_pointer_type, ");
        out.out("baseType: !").out_u64(self->base_type_index).out(", ");
        out.out("size: ").out_u64(self->size).out(") ; ").out(self->name.c_str);
    }
}

pub struct DW_TAG_member {
    base: DI_node,
    name: str,
    scope_index: u64,
    file_index: u64,
    line: i64,
    base_type_index: u64
}

impl DW_TAG_member {
    pub func new(i: u64, n: i8*, si: u64, fi: u64, l: i64, bti: u64) -> DW_TAG_member* {
        var res = DW_TAG_member::__alloc__();
        res->base = DI_node::instance(DI_kind::DW_TAG_member, i);
        res->name = str::from(n);
        res->scope_index = si;
        res->file_index = fi;
        res->line = l;
        res->base_type_index = bti;
        return res;
    }

    pub func delete(self) {
        self->name.delete();
    }

    pub func dump(self, out: io) {
        out.out("!DIDerivedType(tag: DW_TAG_member, ");
        out.out("name: \"").out(self->name.c_str).out("\", ");
        out.out("scope: !").out_u64(self->scope_index).out(", ");
        out.out("file: !").out_u64(self->file_index).out(", ");
        out.out("line: ").out_i64(self->line).out(", ");
        out.out("baseType: !").out_u64(self->base_type_index).out(")");
    }
}