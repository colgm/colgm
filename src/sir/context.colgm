use std::vec::{ primitive_vec, vec };
use std::pair::{ pair };
use std::map::{ hashmap };
use std::ptr::{ basic };
use std::str::{ str };
use std::libc::{ free, streq };
use std::io::{ io };
use std::ptr::{ ptr };

use err::span::{ span };
use sir::sir::*;

use sema::type::{ type };
use dwarf::dwarf::*;

use util::mangling::{ llvm_raw_string };
use util::platform::{ get_platform, get_target_triple };

use util::package::{ package };

pub struct sir_struct {
    name: str,
    location: span,
    field_type: vec<str>
}

impl sir_struct {
    pub func instance(n: str*, loc: span*) -> sir_struct {
        return sir_struct {
            name: n->copy_instance(),
            location: loc->copy_instance(),
            field_type: vec<str>::instance()
        };
    }

    pub func delete(self) {
        self->name.delete();
        self->location.delete();
        self->field_type.delete();
    }

    pub func copy_instance(self) -> sir_struct {
        var res = sir_struct {};
        res.name = self->name.copy_instance();
        res.location = self->location.copy_instance();
        res.field_type = self->field_type.copy_instance();
        return res;
    }

    pub func dump(self, out: io) {
        out.out(self->name.c_str).out(" = type ");

        if (self->field_type.empty()) {
            out.out("{}\n");
            return;
        } else {
            out.out("{ ");
        }

        foreach (var i; self->field_type) {
            if (i.index() != 0) {
                out.out(", ");
            }
            out.out(i.get()->c_str);
        }

        out.out(" }\n");
    }
}

pub struct sir_func {
    name: str,
    params: vec<pair<str, str>>,
    return_type: str,
    attributes: vec<str>,
    with_va_args: bool,
    debug_info_index: u64,
    body: sir_block*
}

impl sir_func {
    pub func instance(n: str*) -> sir_func {
        return sir_func {
            name: n->copy_instance(),
            params: vec<pair<str, str>>::instance(),
            return_type: str::instance(),
            attributes: vec<str>::instance(),
            with_va_args: false,
            debug_info_index: DI_ERROR_INDEX(),
            body: nil
        };
    }

    pub func delete(self) {
        self->name.delete();
        self->params.delete();
        self->return_type.delete();
        self->attributes.delete();
        if (self->body != nil) {
            var n = self->body => sir*;
            n->delete();
            free(n => i8*);
        }
    }

    pub func copy_instance(self) -> sir_func {
        var res = sir_func {};
        res.name = self->name.copy_instance();
        res.params = self->params.copy_instance();
        res.return_type = self->return_type.copy_instance();
        res.attributes = self->attributes.copy_instance();
        res.with_va_args = self->with_va_args;
        res.debug_info_index = self->debug_info_index;

        res.body = self->body; // shallow copy
        self->body = nil; // set to nil to avoid double free
        return res;
    }

    pub func set_attributes(self, v: vec<str>*) {
        self->attributes.clear();
        foreach (var i; v) {
            self->attributes.push(i.get());
        }
    }

    func dump_attributes(self, out: io) {
        var contain_frame_pointer_attribute = false;
        foreach (var i; self->attributes) {
            out.out(" ").out(i.get()->c_str);
            // if frame-pointer attribute is found
            // do not generate frame-pointer=non-leaf for macos
            if (i.get()->find_i8_vec("frame-pointer") != str::npos()) {
                contain_frame_pointer_attribute = true;
            }
        }

        // add frame pointer attribute for macos
        // then backtrace can get symbol of functions
        // but on linux, option `-rdynamic` should be given to ld
        // otherwise this attribute does not work
        if (self->body != nil && !contain_frame_pointer_attribute &&
            streq(get_platform(), "macos")) {
            out.out(" ").out("\"frame-pointer\"=\"non-leaf\"");
        }
    }

    pub func dump(self, out: io) {
        if (self->body == nil) {
            out.out("declare ");
        } else {
            out.out("define ");
        }
        out.out(self->return_type.c_str).out(" @").out(self->name.c_str).out("(");
        foreach (var i; self->params) {
            if (i.index() != 0) {
                out.out(", ");
            }
            out.out(i.get()->value.c_str).out(" %");
            out.out(i.get()->key.c_str);
        }
        if (self->with_va_args) {
            out.out(", ...");
        }
        out.out(")");
        self->dump_attributes(out);

        if (self->debug_info_index != DI_ERROR_INDEX()) {
            out.out(" !dbg !").out_u64(self->debug_info_index);
        }

        if (self->body == nil) {
            out.out("\n");
            return;
        }

        out.out(" {\n");
        var n = self->body => sir*;
        out.out("label.entry:\n");
        n->dump(out);
        out.out("}\n");
    }
}

pub struct sir_context {
    struct_decls: vec<sir_struct>,
    func_decls: vec<sir_func>,
    func_impls: vec<sir_func>,
    const_strings: hashmap<str, u64>,

    named_metadata: vec<DI_named_metadata*>,
    debug_info: vec<DI_node*>,
    DI_file_map: hashmap<str, u64>,
    DI_type_map: hashmap<str, u64>,
    DI_struct_map: hashmap<str, DI_structure_type*>,

    pkg: package*
}

impl sir_context {
    pub func instance(pkg: package*) -> sir_context {
        return sir_context {
            struct_decls: vec<sir_struct>::instance(),
            func_decls: vec<sir_func>::instance(),
            func_impls: vec<sir_func>::instance(),
            const_strings: hashmap<str, u64>::instance(),
            named_metadata: vec<DI_named_metadata*>::instance(),
            debug_info: vec<DI_node*>::instance(),
            DI_file_map: hashmap<str, u64>::instance(),
            DI_type_map: hashmap<str, u64>::instance(),
            DI_struct_map: hashmap<str, DI_structure_type*>::instance(),
            pkg: pkg
        };
    }

    pub func delete(self) {
        self->struct_decls.delete();
        self->func_decls.delete();
        self->func_impls.delete();
        self->const_strings.delete();
        foreach (var i; self->named_metadata) {
            var n = i.get() => DI_node*;
            n->delete();
            free(n => i8*);
        }
        self->named_metadata.delete();
        foreach (var i; self->debug_info) {
            var n = i.get();
            n->delete();
            free(n => i8*);
        }
        self->debug_info.delete();
        self->DI_file_map.delete();
        self->DI_type_map.delete();
        self->DI_struct_map.delete();
    }
}

impl sir_context {
    pub func add_DI_type(self, name: i8*, index: u64) {
        var temp = str::from(name);
        self->DI_type_map.insert(temp.__ptr__(), index);
        temp.delete();
    }

    pub func add_DI_struct(self, name: i8*, node: DI_structure_type*) {
        var temp = str::from(name);
        self->DI_struct_map.insert(
            temp.__ptr__(),
            node
        );
        temp.delete();
    }
}

impl sir_context {
    func dump_target_tripple(self, out: io) {
        out.out("target triple = \"").out(get_target_triple()).out("\"\n\n");
    }

    func dump_structs(self, out: io) {
        foreach (var i; self->struct_decls) {
            i.get()->dump(out);
        }

        if (self->struct_decls.size > 0) {
            out.out("\n");
        }
    }

    func dump_func_decls(self, out: io) {
        foreach (var i; self->func_decls) {
            i.get()->dump(out);
        }

        if (self->func_decls.size > 0) {
            out.out("\n");
        }
    }

    func dump_func_impls(self, out: io) {
        foreach (var i; self->func_impls) {
            i.get()->dump(out);
            out.endln();
        }
    }

    func dump_const_strings(self, out: io) {
        var ordered_const_string = vec<str>::instance();
        foreach (var i; self->const_strings) {
            var t = str::instance();
            ordered_const_string.push(t.__ptr__());
            t.delete();
        }

        foreach (var i; self->const_strings) {
            var s_value = i.key();
            var s_index = i.value();

            var s_ptr = ordered_const_string.get(s_index);
            s_ptr->append_str(s_value);
        }

        foreach (var i; ordered_const_string) {
            out.out("@str.").out_u64(i.index());
            out.out(" = private unnamed_addr constant [");
            out.out_u64(i.get()->size + 1).out(" x i8] c\"");

            var raw_str = llvm_raw_string(i.get());
            out.out(raw_str.c_str).out("\"\n");
            raw_str.delete();
        }

        if (!ordered_const_string.empty()) {
            out.out("\n");
        }

        ordered_const_string.delete();
    }

    func dump_struct_size_method(self, out: io) {
        foreach (var i; self->struct_decls) {
            var st_name = i.get()->name.copy_instance();
            var size_func_name = str::instance();
            if (st_name.size >= 2 && st_name.get(1) == '"') {
                var temp = st_name.substr_from(9);
                size_func_name.append_i8_vec("\"");
                size_func_name.append_str(temp.__ptr__());
                temp.delete();
            } else {
                var temp = st_name.substr_from(8);
                size_func_name.append_str(temp.__ptr__());
                temp.delete();
            }
            if (size_func_name.back() == '"') {
                size_func_name.pop_back();
                size_func_name.append_i8_vec(".__size__\"");
            } else {
                size_func_name.append_i8_vec(".__size__");
            }

            out.out("define i64 @").out(size_func_name.c_str);
            out.out("() alwaysinline {\n");
            out.out("label.entry:\n");
            out.out("  ret i64 ptrtoint (").out(st_name.c_str);
            out.out("* getelementptr (").out(st_name.c_str).out(", ");
            out.out(st_name.c_str).out("* null, i64 1) to i64)\n}\n\n");

            st_name.delete();
            size_func_name.delete();
        }
    }

    func dump_struct_alloc_method(self, out: io) {
        foreach (var i; self->struct_decls) {
            var st_name = i.get()->name.copy_instance();
            var size_func_name = str::instance();
            var alloc_func_name = str::instance();
            if (st_name.size >= 2 && st_name.get(1) == '"') {
                var temp = st_name.substr_from(9);
                size_func_name.append_i8_vec("\"");
                size_func_name.append_str(temp.__ptr__());
                alloc_func_name.append_i8_vec("\"");
                alloc_func_name.append_str(temp.__ptr__());
                temp.delete();
            } else {
                var temp = st_name.substr_from(8);
                size_func_name.append_str(temp.__ptr__());
                alloc_func_name.append_str(temp.__ptr__());
                temp.delete();
            }
            if (size_func_name.back() == '"') {
                size_func_name.pop_back();
                size_func_name.append_i8_vec(".__size__\"");
                alloc_func_name.pop_back();
                alloc_func_name.append_i8_vec(".__alloc__\"");
            } else {
                size_func_name.append_i8_vec(".__size__");
                alloc_func_name.append_i8_vec(".__alloc__");
            }

            out.out("define ").out(st_name.c_str).out("* @");
            out.out(alloc_func_name.c_str).out("() alwaysinline {\n");
            out.out("label.entry:\n");
            out.out("  %0 = call i64 @").out(size_func_name.c_str).out("()\n");
            out.out("  %1 = call i8* @malloc(i64 %0)\n");
            out.out("  %2 = bitcast i8* %1 to ").out(st_name.c_str).out("*\n");
            out.out("  ret ").out(st_name.c_str).out("* %2\n}\n\n");

            st_name.delete();
            size_func_name.delete();
            alloc_func_name.delete();
        }
    }

    func dump_named_metadata(self, out: io) {
        foreach (var i; self->named_metadata) {
            i.get()->dump(out);
            out.endln();
        }
        if (self->named_metadata.size > 0) {
            out.endln();
        }
    }

    func dump_debug_info(self, out: io) {
        foreach (var i; self->debug_info) {
            i.get()->dump(out);
            out.endln();
        }
    }

    pub func dump(self, out: io) {
        self->dump_target_tripple(out);
        self->dump_structs(out);
        self->dump_func_decls(out);
        self->dump_const_strings(out);
        self->dump_struct_size_method(out);
        self->dump_struct_alloc_method(out);
        self->dump_func_impls(out);
        self->dump_named_metadata(out);
        self->dump_debug_info(out);
    }
}