use sir::sir::*;
use sir::context::{ sir_func, sir_context };

use std::vec::{ primitive_vec };
use std::ptr::{ ptr };
use std::str::{ str };
use std::io::{ io };
use std::libc::{ free };

use util::timestamp::{ maketimestamp };

func adjust_single_function(f: sir_func*) -> i64 {
    var replace_count = 0;
    var temp = primitive_vec<ptr<sir>>::instance();
    foreach (var i; f->body->stmts) {
        var inst = i.get().unwrap();
        if (inst->kind != sir_kind::sir_call) {
            temp.push(ptr<sir>::wrap(inst));
            continue;
        }

        var call = inst => sir_call*;
        if (call->name.find_i8_vec(".__ptr__") == str::npos()) {
            temp.push(ptr<sir>::wrap(inst));
            continue;
        }

        // __ptr__ call has one argument, and must return pointer type
        // __ptr__(self: T*) -> T*
        if (call->args.size != 1 ||
            call->return_type.empty() ||
            call->return_type.back() != '*') {
            temp.push(ptr<sir>::wrap(inst));
            continue;
        }

        var rtt = call->return_type.copy_instance();
        rtt.pop_back(); // delete tail *

        replace_count += 1;
        // replace
        //   %target = call %struct.T* @T.__ptr__(%struct.T* %source)
        // to
        //   %target = getelementptr %struct.T, %struct.T* %source, i32 0; __ptr__ call
        var constant = sir_temp_ptr::new(
            call->target.content.__ptr__(),
            call->args.get(0)->content.__ptr__(),
            rtt.__ptr__(),
            "__ptr__ call"
        );
        temp.push(ptr<sir>::wrap(constant => sir*));

        rtt.delete();
        inst->delete();
        free(inst => i8*);
    }

    f->body->stmts.clear();
    foreach (var i; temp) {
        f->body->stmts.push(i.get());
    }

    temp.delete();
    return replace_count;
}

pub func replace_struct_ptr_call(ctx: sir_context*, verbose: bool) {
    var ts = maketimestamp();
    ts.stamp();

    var replace_count = 0;
    foreach (var i; ctx->func_impls) {
        replace_count += adjust_single_function(i.get());
    }

    if (verbose) {
        io::stdout().green().out("[ SIR-PASS ] ").reset();
        io::stdout().out("Run replace __ptr__ call: ");
        io::stdout().light_cyan().out_i64(replace_count).reset();
        io::stdout().out(" ").out_f64(ts.elapsed_msec()).out(" ms\n");
    }
}