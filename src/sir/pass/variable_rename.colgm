use sir::sir::*;
use sir::context::{ sir_func, sir_context };
use sir::value::{ value_kind, value_t };

use std::str::{ str };
use std::io::{ io };
use std::map::{ hashmap };
use std::util::timestamp::{ maketimestamp };

struct ssa_replace_context {
    params: hashmap<str, str>,
    name_map: hashmap<str, str>,
    replace_count: i64
}

impl ssa_replace_context {
    pub func instance() -> ssa_replace_context {
        return ssa_replace_context {
            params: hashmap<str, str>::instance(),
            name_map: hashmap<str, str>::instance(),
            replace_count: 0
        };
    }

    pub func delete(self) {
        self.params.delete();
        self.name_map.delete();
    }

    pub func clear(self) {
        self.params.clear();
        self.name_map.clear();
        self.replace_count = 0;
    }

    pub func add_param(self, name: str&) {
        self.params.insert(name, name);
    }

    pub func rename(self, name: value_t&) {
        if (name.kind != value_kind::variable) {
            return;
        }

        if (self.params.has(name.content)) {
            return;
        }

        if (self.name_map.has(name.content)) {
            var new_name = self.name_map.get(name.content);
            name.content.delete();
            name.content = new_name.clone();
            self.replace_count += 1;
            return;
        }

        var new_name = str::from_u64(self.name_map.size);
        self.name_map.insert(name.content, new_name);

        name.content.delete();
        name.content = new_name;
        self.replace_count += 1;
    }

    pub func accept(self, stmt: sir*) {
        match (stmt->kind) {
            sir_kind::sir_null => {}
            sir_kind::sir_block => {}
            sir_kind::sir_alloca => {
                var n = stmt => sir_alloca*;
                self.rename(n->name);
            }
            sir_kind::sir_ret => {
                var n = stmt => sir_ret*;
                self.rename(n->value);
            }
            sir_kind::sir_str => {
                var n = stmt => sir_str*;
                self.rename(n->target);
            }
            sir_kind::sir_zeroinitializer => {
                var n = stmt => sir_zeroinitializer*;
                self.rename(n->target);
            }
            sir_kind::sir_get_index => {
                var n = stmt => sir_get_index*;
                self.rename(n->source);
                self.rename(n->index);
                self.rename(n->target);
            }
            sir_kind::sir_get_field => {
                var n = stmt => sir_get_field*;
                self.rename(n->source);
                self.rename(n->target);
            }
            sir_kind::sir_call => {
                var n = stmt => sir_call*;
                foreach (var i; n->args) {
                    self.rename(i.get());
                }
                self.rename(n->target);
            }
            sir_kind::sir_neg => {
                var n = stmt => sir_neg*;
                self.rename(n->source);
                self.rename(n->target);
            }
            sir_kind::sir_bnot => {
                var n = stmt => sir_bnot*;
                self.rename(n->source);
                self.rename(n->target);
            }
            sir_kind::sir_lnot => {
                var n = stmt => sir_lnot*;
                self.rename(n->source);
                self.rename(n->target);
            }
            sir_kind::sir_add => {
                var n = stmt => sir_add*;
                self.rename(n->left);
                self.rename(n->right);
                self.rename(n->target);
            }
            sir_kind::sir_fadd => {
                var n = stmt => sir_fadd*;
                self.rename(n->left);
                self.rename(n->right);
                self.rename(n->target);
            }
            sir_kind::sir_sub => {
                var n = stmt => sir_sub*;
                self.rename(n->left);
                self.rename(n->right);
                self.rename(n->target);
            }
            sir_kind::sir_mul => {
                var n = stmt => sir_mul*;
                self.rename(n->left);
                self.rename(n->right);
                self.rename(n->target);
            }
            sir_kind::sir_div => {
                var n = stmt => sir_div*;
                self.rename(n->left);
                self.rename(n->right);
                self.rename(n->target);
            }
            sir_kind::sir_rem => {
                var n = stmt => sir_rem*;
                self.rename(n->left);
                self.rename(n->right);
                self.rename(n->target);
            }
            sir_kind::sir_band => {
                var n = stmt => sir_band*;
                self.rename(n->left);
                self.rename(n->right);
                self.rename(n->target);
            }
            sir_kind::sir_bxor => {
                var n = stmt => sir_bxor*;
                self.rename(n->left);
                self.rename(n->right);
                self.rename(n->target);
            }
            sir_kind::sir_bor => {
                var n = stmt => sir_bor*;
                self.rename(n->left);
                self.rename(n->right);
                self.rename(n->target);
            }
            sir_kind::sir_cmp => {
                var n = stmt => sir_cmp*;
                self.rename(n->left);
                self.rename(n->right);
                self.rename(n->target);
            }
            sir_kind::sir_basic_block => {}
            sir_kind::sir_store => {
                var n = stmt => sir_store*;
                self.rename(n->source);
                self.rename(n->target);
            }
            sir_kind::sir_load => {
                var n = stmt => sir_load*;
                self.rename(n->source);
                self.rename(n->target);
            }
            sir_kind::sir_br => {}
            sir_kind::sir_br_cond => {
                var n = stmt => sir_br_cond*;
                self.rename(n->cond);
            }
            sir_kind::sir_switch => {
                var n = stmt => sir_switch*;
                self.rename(n->source);
            }
            sir_kind::sir_type_convert => {
                var n = stmt => sir_type_convert*;
                self.rename(n->source);
                self.rename(n->target);
            }
            sir_kind::sir_array_cast => {
                var n = stmt => sir_array_cast*;
                self.rename(n->source);
                self.rename(n->target);
            }
        }
    }
}

pub func variable_rename_to_form_ssa(ctx: sir_context*, verbose: bool) {
    var ts = maketimestamp();
    ts.stamp();

    var rc = ssa_replace_context::instance();
    defer rc.delete();

    var replace_count = 0;
    foreach (var i; ctx->func_impls) {
        rc.clear();
        foreach (var j; i.get().params) {
            rc.add_param(j.get().key);
        }
        foreach (var j; i.get().body->basic_block) {
            foreach (var k; j.get()->stmts) {
                rc.accept(k.get());
            }
        }
        replace_count += rc.replace_count;
    }

    if (verbose) {
        io::stdout().green().out("  SIR-PASS ").reset();
        io::stdout().out("Run pass");
        io::stdout().blue().out(" <variable rename>").reset().out(": ");
        io::stdout().cyan().out_i64(replace_count).reset();
        io::stdout().out(" ").out_f64(ts.elapsed_msec()).out(" ms\n");
    }
}