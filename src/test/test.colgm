use std::libc::{ free, itoa, time, srand, rand };
use std::list::{ list, primitive_list };
use std::vec::{ vec, primitive_vec };
use std::map::{ hashmap };
use std::set::{ hashset };
use std::str::{ str };
use std::io::{ io };

func test_set() {
    var buff = [i8; 1024];
    var set = hashset<str>::instance();
    for (var i = 0; i < 128; i += 1) {
        itoa(i, buff, 10);
        var n = str::instance();
        n.append_i8_vec(buff);
        set.insert(n.__ptr__());
        n.delete();
    }

    for (var i = set.iter(); !i.is_end(); i = i.next()) {
        var n = i.elem();
        if (set.has(n)) {
            io::stdout().out("[").green().out("test").reset().out("] hashset: ");
            io::stdout().out(n->c_str).out(" found in bucket ");
            io::stdout().out_i64(i.bucket_index => i64).endln();
        } else {
            io::stdout().out("[").red().out("test").reset().out("] hashset: ");
            io::stdout().out(n->c_str).out(" not found\n");
        }
    }

    set.delete();
}

func test_hashmap() {
    var buff = [i8; 1024];
    var map = hashmap<str, str>::instance();
    var stored = vec<str>::instance();

    for (var i = 0; i < 128; i += 1) {
        var num = rand() % 4096;
        itoa(num => i64, buff, 10);
        var k = str::instance();
        var v = str::instance();
        k.append_i8_vec(buff);
        v.append_i8_vec("[test hashmap<str, str> ");
        v.append_i8_vec(buff)->append_i8_vec("]");
        map.insert(k.__ptr__(), v.__ptr__());
        stored.push(k.__ptr__());
        k.delete();
        v.delete();
    }

    for (var i = 0; i < 128; i += 1) {
        var key = stored.get(i => u64);
        if (map.has(key)) {
            io::stdout().out("[").green().out("test").reset().out("] hashmap ");
            io::stdout().out_i64(i).out(": key ");
            io::stdout().out(key->c_str).out(" found -> ");
            io::stdout().out(map.get(key)->c_str).endln();
        } else {
            io::stdout().out("[").red().out("test").reset().out("] hashmap ");
            io::stdout().out_i64(i).out(": ");
            io::stdout().out(key->c_str).out(" not found\n");
        }
    }

    for (var i = map.iter(); !i.is_end(); i = i.next()) {
        io::stdout().out("[").green().out("test").reset();
        io::stdout().out("] hashmap::iter ");
        io::stdout().out(i.key()->c_str).out(" -> ").out(i.value()->c_str);
        io::stdout().out(" in bucket ").out_i64(i.bucket_index => i64).out("\n");
    }

    map.delete();
    stored.delete();
}

func list_dump(list: list<str>*) {
    for (var i = list->iter(); !i.is_end(); i = i.next()) {
        io::stdout().out("[").green().out("test").reset().out("] list: ");
        io::stdout().out(i.elem()->c_str).endln();
    }
}

func primitive_list_dump(list: primitive_list<i64>*) {
    var temp = list->head;
    while (temp != nil) {
        io::stdout().out("[").green().out("test").reset().out("] list: ");
        io::stdout().out_i64(temp->elem).endln();
        temp = temp->next;
    }
}

func test_list() {
    var s = str::instance();
    var str_list = list<str>::instance();
    var int_list = primitive_list<i64>::instance();

    for (var i = 0; i < 32; i += 1) {
        var num = rand() % 4096;
        var buff = [i8; 1024];
        itoa(num => i64, buff, 10);
        s.clear();
        s.append_i8_vec("test list<str> ");
        s.append_i8_vec(buff);
        str_list.insert(s.__ptr__());
        int_list.insert(num => i64);
    }
    list_dump(str_list.__ptr__());
    primitive_list_dump(int_list.__ptr__());

    s.delete();
    str_list.delete();
    int_list.delete();
}

func vec_dump(vec: vec<str>*) {
    for (var i: u64 = 0; i < vec->size; i += 1) {
        io::stdout().out("[").green().out("test").reset().out("] vec: ");
        io::stdout().out(vec->get(i)->c_str).endln();
    }
}

func primitive_vec_dump(vec: primitive_vec<i64>*) {
    for (var i: u64 = 0; i < vec->size; i += 1) {
        io::stdout().out("[").green().out("test").reset().out("] vec: ");
        io::stdout().out_i64(vec->get(i)).endln();
    }
}

func test_vector() {
    var s = str::instance();
    var str_vec = vec<str>::instance();
    var int_vec = primitive_vec<i64>::instance();
    for (var i = 0; i < 32; i += 1) {
        var num = rand() % 4096;
        var buff = [i8; 1024];
        itoa(num => i64, buff, 10);
        s.clear();
        s.append_i8_vec("test vec<str> ");
        s.append_i8_vec(buff);
        str_vec.push(s.__ptr__());
        int_vec.push(num => i64);
    }
    vec_dump(str_vec.__ptr__());
    primitive_vec_dump(int_vec.__ptr__());

    s.delete();
    str_vec.delete();
    int_vec.delete();
}

func TEST_ALL() {
    srand(time(nil) => u32);
    test_hashmap();
    test_list();
    test_vector();
    test_set();
}

func main() -> i32 {
    TEST_ALL();
    return 0;
}